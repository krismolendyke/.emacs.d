#+TITLE: Literate =.emacs.d=
#+OPTIONS: toc:nil num:nil

#+BEGIN_QUOTE
[[http://www.literateprogramming.com/][Literate programming]] is a methodology that combines a programming language
with a documentation language, thereby making programs more robust, more
portable, more easily maintained, and arguably more fun to write than programs
that are written only in a high-level language. The main idea is to treat a
program as a piece of literature, addressed to human beings rather than to a
computer. The program is also viewed as a hypertext document, rather like the
World Wide Web. (Indeed, I used the word WEB for this purpose long before CERN
grabbed it!) -- [[http://www-cs-faculty.stanford.edu/~uno/lp.html][Donald Knuth]]
#+END_QUOTE

All configuration described here is performed in an [[https://github.com/krismolendyke/.emacs.d/blob/0d5a5434ff79d48ab613fc433d0ae2443c552665/init.el#L88][=after-init-hook=]]
function.

#+TOC: headlines 2

* Build Emacs
  :PROPERTIES:
  :CUSTOM_ID: build-emacs
  :END:

  I primarily use Emacs locally on Apple hardware.  Most, if not all,
  of the remote editing that I do is done via =tramp=.  I keep an eye
  on the [[http://git.savannah.gnu.org/cgit/emacs.git/log/][log]] at the official Emacs [[http://git-scm.com/][git]] [[http://git.savannah.gnu.org/cgit/emacs.git/][repository]].  When something
  interesting lands there I will build a fresh Emacs installation via
  [[http://brew.sh/][Homebrew]] and try it out.

  #+BEGIN_SRC sh
    brew info emacs
  #+END_SRC

  I install Emacs with the following options.  The standard Homebrew
  installation has lost a lot of MacOS customization flags over the
  years so I've switched to the [[https://github.com/d12frosted/homebrew-emacs-plus][Emacs Plus]] formula instead.  First,
  tap that keg:

  #+BEGIN_SRC sh
    brew tap d12frosted/emacs-plus
  #+END_SRC

  And then install:

  #+BEGIN_SRC sh
    brew install emacs-plus@27 --with-jansson
  #+END_SRC

  The =--with-jansson= flag enables a native JSON serdes library that
  is supposed to improve JSON support.  I am mainly interested in LSP
  performance improvements enabled by compiling with this flag.

  If there is an existing installation it needs to be removed first.

  #+BEGIN_SRC sh
    brew uninstall emacs
  #+END_SRC

  If there is a major version bump between the previous and current
  build [[#cask][Cask]] will need to be updated.

  #+BEGIN_SRC sh
    cd ~/.emacs
    cask clean-elc
    cask update
    cask install
  #+END_SRC

** Emacs Source Directory

   =source-directory= is helpful to have set properly when exploring built-in
   /C functions/ via =find-function=.

   Finding the source code directory via Homebrew can be done with the
   following command:

   #+BEGIN_SRC sh :exports both
     echo $(brew --cache)/emacs*
   #+END_SRC

   #+RESULTS:
   : /Library/Caches/Homebrew/emacs--git

   /Note/: =source-directory= must be set during Emacs initialization time to
   persist.  See [[https://github.com/krismolendyke/.emacs.d/blob/1241a848cee7dadfa0c719643925fa0a7b86f476/init.el#L84-L86][=init.el=]].

* How this works
  :PROPERTIES:
  :CUSTOM_ID: how-this-works
  :END:

  The [[https://github.com/krismolendyke/.emacs.d/blob/master/init.el][=init.el=]] file bootstraps the entire process.  It is found by
  Emacs automatically if it is located in one of several well-defined
  locations.

  #+BEGIN_QUOTE
  When Emacs is started, it normally tries to load a Lisp program from
  an "initialization file", or "init file" for short.  This file, if
  it exists, specifies how to initialize Emacs for you.  Emacs looks
  for your init file using the filenames `~/.emacs', `~/.emacs.el', or
  `~/.emacs.d/init.el'; you can choose to use any one of these three
  names (*note Find Init::).  Here, `~/' stands for your home
  directory. -- [[http://www.gnu.org/software/emacs/manual/html_node/emacs/Init-File.html][The Emacs Initialization File]]
  #+END_QUOTE

  Once it's found, =init.el= sets "global" variables and defines
  several functions to be executed /during/ initialization and one
  that should be executed /after/ initialization has completed.
  =k20e/after-init-hook= is a hook run after initialization.  The most
  interesting bit of this function is the call to
  =org-babel-load-file=:

  #+BEGIN_QUOTE
  (org-babel-load-file FILE &optional COMPILE)

  Load Emacs Lisp source code blocks in the Org-mode FILE. This
  function exports the source code using `org-babel-tangle' and then
  loads the resulting file using `load-file'.  With prefix arg
  (noninteractively: 2nd arg) COMPILE the tangled Emacs Lisp file to
  byte-code before it is loaded.
  #+END_QUOTE

  This function invocation exports all [[http://orgmode.org/manual/Working-With-Source-Code.html#Working-With-Source-Code][=org-mode= source blocks]] in any
  =org-mode= files in the =.emacs.d= directory to a single Emacs Lisp
  file and then instructs Emacs to load that file.  [[https://github.com/krismolendyke/.emacs.d/blob/master/custom.org][=custom.org=]] is
  the biggest of those =org-mode= files and ‚Ä¶ /this/ is that file!

** HTML Export =post-commit= Hook

   This [[https://git-scm.com/book/en/v2/Customizing-Git-Git-Hooks][git =post-commit= hook]] is located at =.git/hooks/post-commit=
   and automatically generates the HTML content of this [[https://pages.github.com/][GitHub Pages]]
   site.

   #+BEGIN_SRC sh
     #!/bin/sh

     emacs --batch \
           --directory '~/.emacs.d/site-lisp/org-mode/lisp' \
           --load '~/.emacs.d/elisp/k20e-org-html-export.el' \
           --visit '~/.emacs.d/custom.org' \
           --execute '(org-html-export-to-html)' 2>&1
     rm -f *~
     git checkout gh-pages
     mv custom.html index.html
     git add index.html
     git commit --message "post-commit hook regeneration."
     git checkout master
   #+END_SRC

   All of the interesting customization of the exported document is
   performed in [[https://github.com/krismolendyke/.emacs.d/blob/master/elisp/k20e-org-html-export.el][=elisp/k20e-org-html-export.el=]].

* Customization

  I don't make much use of the [[http://www.gnu.org/software/emacs/manual/html_node/emacs/Customization.html#Customization][Customization]] interface.  By default it dumps
  its settings into =init.el= and I don't want to see them there.  A few
  packages that I use do rely on customization so I do need it to be loaded.

  #+BEGIN_SRC emacs-lisp
    (setq custom-file (expand-file-name ".emacs-custom.el" user-emacs-directory))
    (load custom-file)
  #+END_SRC

* Cask
  :PROPERTIES:
  :CUSTOM_ID: cask
  :END:

  #+BEGIN_QUOTE
  Cask is a project management tool for Emacs Lisp to automate the
  package development cycle; development, dependencies, testing,
  building, packaging and more.

  Cask can also be used to manage dependencies for your local Emacs
  configuration. -- [[http://cask.readthedocs.org/en/latest/][Cask documentation]]
  #+END_QUOTE

** Installation

   #+BEGIN_SRC sh
     brew install cask
   #+END_SRC

   After building a new Emacs version =cask= should be used to update
   dependencies.

   #+BEGIN_SRC sh
     cd ~/.emacs.d
     cask install
   #+END_SRC

   New dependencies will be installed into =~/.emacs.d/.cask=.

* Pallet

  #+BEGIN_QUOTE
  Pallet is a package management helper for Emacs. -- [[https://github.com/rdallasgray/pallet][Pallet README]]
  #+END_QUOTE

  As packages are installed, updated or removed via =list-packages=,
  Pallet maintains changes to the =Cask= file automatically.

* use-package

  Before =use-package= I had 70 packages installed and
  =emacs-init-time= was ="8.678111 seconds"=.

  #+BEGIN_SRC emacs-lisp
    (eval-when-compile (require 'use-package))
  #+END_SRC

* Performance Tuning

  Mucking with these solely based on output from =lsp-doctor= and its
  recommendations since I use this all day writing Go programs.

** GC

   #+BEGIN_SRC emacs-lisp
     (eval-when-compile
       (let ((mib (expt 2 20)))
         (setq gc-cons-threshold (* 500 mib))))
   #+END_SRC

** Process Output

   #+BEGIN_SRC emacs-lisp
     (eval-when-compile
       (let ((mib (expt 2 20)))
         (setq read-process-output-max (* 1 mib))))
   #+END_SRC

* Global GNU Emacs Key Bindings

  These global key bindings override /built-in/ functions only.
  Package-specific or custom function defunition key bindings are made in
  their own dedicated sections where other specific settings are made.

** Unset

   OS X annoyance -- =C-M-d= is a "hot key" bound to dictionary lookup
   and masks the key binding in Emacs.  [[http://apple.stackexchange.com/questions/22785/how-do-i-disable-the-command-control-d-word-definition-keyboard-shortcut-in-os-x/114269#114269][Disabling it]] can currently
   only be done by editing a default /and restarting/.

   #+BEGIN_SRC sh
     defaults write com.apple.symbolichotkeys AppleSymbolicHotKeys \
              -dict-add 70 '<dict><key>enabled</key><false/></dict>'
   #+END_SRC

   #+BEGIN_SRC emacs-lisp
     (global-unset-key (kbd "<f1> h"))
     (global-unset-key (kbd "<f11>"))
     (global-unset-key (kbd "C-h"))
     (global-unset-key (kbd "C-q"))
     (global-unset-key (kbd "M-`"))
     (global-unset-key (kbd "M-c"))
     (global-unset-key (kbd "M-h"))
     (global-unset-key (kbd "M-u"))
   #+END_SRC

** Set

   #+BEGIN_SRC emacs-lisp
     (global-set-key (kbd "<f1> F") 'find-function)
     (global-set-key (kbd "<f1> V") 'find-variable)
     (global-set-key (kbd "<f6>") 'k20e/font-toggle-proportional)
     (global-set-key (kbd "<f7>") 'previous-error) ;; ‚óÄ‚óÄ
     (global-set-key (kbd "<f9>") 'next-error) ;; ‚ñ∂‚ñ∂
     (global-set-key (kbd "C-M-;") 'comment-line)
     (global-set-key (kbd "C-S-h") 'kill-whole-line)
     (global-set-key (kbd "C-c DEL") 'join-line)
     (global-set-key (kbd "C-h") 'delete-backward-char)
     (global-set-key (kbd "C-j") 'join-line)
     (global-set-key (kbd "C-x C-t") 'transpose-lines)
     (global-set-key (kbd "H-h H-f") 'find-function)
     (global-set-key (kbd "H-h H-v") 'find-variable)
     (global-set-key (kbd "H-t") 'toggle-frame-fullscreen)
     (global-set-key (kbd "M-+") 'text-scale-adjust)
     (global-set-key (kbd "M-.") 'imenu)
     (global-set-key (kbd "M-/") 'hippie-expand)
     (global-set-key (kbd "M-`") 'other-window)
     (global-set-key (kbd "M-h") 'backward-kill-word)
     (global-set-key (kbd "M-t") 'transpose-words)
   #+END_SRC

* k20e Defaults

** Apropos

   Sort by relevancy.

   #+BEGIN_SRC emacs-lisp
     (setq-default apropos-sort-by-scores t)
   #+END_SRC

** =cycle-spacing=

   #+BEGIN_SRC emacs-lisp
     (defun k20e/cycle-spacing (&optional n)
       "Make `cycle-spacing' operate in `fast' mode."
       (interactive "*p")
       (cycle-spacing n nil 'fast))
   #+END_SRC

** Backup Files

   Back up files to a single location.

   #+BEGIN_SRC emacs-lisp
     (defvar k20e/backup-dir (expand-file-name "backup" user-emacs-directory)
       "A single directory for storing backup files within.")

     (unless (file-exists-p k20e/backup-dir) (make-directory k20e/backup-dir))

     (setq backup-by-copying t
           backup-directory-alist `(("." . ,k20e/backup-dir))
           delete-old-versions t
           version-control t)
   #+END_SRC

** Enabled Commands

   Commands disabled by default prompt at first use.  Enabling
   commands disables the prompt.

   #+BEGIN_SRC emacs-lisp
     (defvar k20e/enabled-commands
       '(downcase-region
         upcase-region
         narrow-to-region
         narrow-to-page
         scroll-left
         scroll-right)
       "Normally disabled commands.")

     (defun k20e/enable-commands ()
       "Enabled normally disabled commands."
       (dolist (command k20e/enabled-commands)
         (put command 'disabled nil)))

     (k20e/enable-commands)
   #+END_SRC

** Inferior Shell

   Defaulting to =sh= seems to work well.

   #+BEGIN_SRC emacs-lisp
     (setq shell-file-name "/bin/sh")
   #+END_SRC

** TODO Defaults for Review

   This is a bunch of stuff that I just dumped here and need to go through yet.

   Show the active region and delete it when selected if a character
   is inserted.

   #+BEGIN_SRC emacs-lisp
     (transient-mark-mode t)
     (delete-selection-mode 1)
   #+END_SRC

   "Electric" indentation is generally what I consider to be sensible.

   #+BEGIN_SRC emacs-lisp
     (electric-indent-mode)
   #+END_SRC

   Cycle through the mark ring faster.

   #+BEGIN_SRC emacs-lisp
     (setq set-mark-command-repeat-pop t)
   #+END_SRC

   Splitting windows horizontally makes more sense on all of the wide
   screen monitors I work on.

   #+BEGIN_SRC emacs-lisp
     (setq split-width-threshold 81)
   #+END_SRC

   #+BEGIN_SRC emacs-lisp
     ;; What's going on here?
     (setq echo-keystrokes 0.1)

     ;; Automatically reload buffers when files change on disk.
     (global-auto-revert-mode 1)

     ;; y is the new yes.  n is the new no.
     (defalias 'yes-or-no-p 'y-or-n-p)
   #+END_SRC

* k20e Custom Functions

  I have found these to be useful enough to keep around permanently.

** Editing

   #+BEGIN_SRC emacs-lisp
     (defun k20e/mark-current-line (arg)
       "Mark the current line.
     If the mark is already set simply move the point forward a single
     line.  If it is not set, set it at the beginning of the current
     line and then move the point forward a single line."
       (interactive "p")
       (unless mark-active
         (beginning-of-line)
         (set-mark (point)))
       (forward-line arg))

     (defun k20e/open-line-below (arg)
       "Insert a new line below the current line."
       (interactive "p")
       (end-of-line)
       (newline arg)
       (indent-for-tab-command))

     (defun k20e/open-line-above (arg)
       "Insert a new line above the current line."
       (interactive "p")
       (beginning-of-line)
       (newline arg)
       (forward-line (- 0 arg))
       (indent-for-tab-command))

     ;; Inspired by http://whattheemacsd.com/key-bindings.el-01.html
     (require 'display-line-numbers)
     (defun k20e/goto-line ()
       "Show line numbers and prompt for a line number to go to.
     Restore previous state of displaying line numbers."
       (interactive)
       (if display-line-numbers-mode
         (call-interactively 'goto-line)
         (unwind-protect
             (progn
               (setq display-line-numbers t)
               (call-interactively 'goto-line))
           (setq display-line-numbers nil))))

   #+END_SRC

   This one is stolen from [[https://github.com/magnars/.emacs.d/blob/e56e71ce0f0791c7237192a049f29c2de686409a/defuns/lisp-defuns.el][magnars]]:

   #+BEGIN_SRC emacs-lisp
     (defun k20e/eval-and-replace ()
       "Replace the preceding sexp with its value."
       (interactive)
       (backward-kill-sexp)
       (condition-case nil
           (prin1 (eval (read (current-kill 0)))
                  (current-buffer))
         (error (message "Invalid expression")
                (insert (current-kill 0)))))
   #+END_SRC

   Bind editing functions:

   #+BEGIN_SRC emacs-lisp
     (global-set-key (kbd "M-l") 'k20e/mark-current-line)
     (global-set-key (kbd "<M-return>") 'k20e/open-line-below)
     (global-set-key (kbd "<M-S-return>") 'k20e/open-line-above)
     (global-set-key [remap goto-line] 'k20e/goto-line)
   #+END_SRC

** Buffers

   #+BEGIN_SRC emacs-lisp
     (defun k20e/display-buffer-file-name ()
       "Message the full path to the currently visited file."
       (interactive)
       (message "%s" (buffer-file-name)))
   #+END_SRC

   #+BEGIN_SRC emacs-lisp
     (defun k20e/font-toggle-proportional ()
       "Toggle between a proportional font."
       (interactive)
       (buffer-face-toggle '(:family "PragmataPro-Liga")))
   #+END_SRC

*** Toggle Source/Test Buffer

    If this gets any smarter it should be refactored into its own
    package.

    #+BEGIN_SRC emacs-lisp
      (defun k20e/test-buffer-p ()
        "Is the current buffer a test buffer?
      This function na√Øvely assumes that the file name suffix '_test'
      is indicative of a test file."
        (string-suffix-p
         "_test"
         (file-name-sans-extension (buffer-file-name))))

      (defun k20e/switch-to-test-buffer ()
        "Switch to the test buffer associated with the current source buffer.
      FIX: when >1 buffer w/ same name this is wrong because the buffer name is prepended w/ dir name or whatever"
        (let ((d (file-name-directory (buffer-file-name)))
              (f (format "%s_test.%s"
                          (file-name-sans-extension (buffer-name))
                          (file-name-extension (buffer-file-name)))))
          (find-file (expand-file-name f d))))

      (defun k20e/switch-to-source-buffer ()
        "Switch to the source buffer associated with the current test buffer."
        (let ((e (file-name-extension (buffer-file-name)))
              (f (car (split-string (file-name-sans-extension (buffer-file-name))
                                    "_test"))))
          (find-file (format "%s.%s" f e))))

      (defun k20e/toggle-test-buffer ()
        "Toggle between a source and test buffer.
      This function na√Øvely assumes that the file name suffix '_test'
      is indicative of a test file.  Therefore it should only be useful
      in major modes where that convention is expected."
        (interactive)
        (if (k20e/test-buffer-p)
            (k20e/switch-to-source-buffer)
          (k20e/switch-to-test-buffer)))
    #+END_SRC

*** Widescreen

    When working on a widescreen monitor it can be useful to have
    windows arranged a bit differently than they would on smaller
    monitors.  In particular, a function like =fit-window-to-buffer=
    which adjusts the window's width is helpful.

    #+BEGIN_SRC emacs-lisp
      (defun k20e/get-longest-line-length ()
        "Get the length of the longest line in the selected window."
        (save-excursion
          (goto-char (point-min))
          (let ((max-length 0)
                (last-line (count-lines (point-min) (point-max))))
            (while (<= (line-number-at-pos) last-line)
              (setq max-length (max max-length (- (point-at-eol) (point-at-bol))))
              (forward-line))
            (1+ max-length))))

      (defun k20e/fit-window-to-buffer-horizontally ()
        "Fit the selected window to the width of its longest line.
      Return the window width delta."
        (interactive)
        (let* ((current-width (window-width))
               (longest-line (k20e/get-longest-line-length))
               (delta (* -1 (- current-width longest-line))))
          (if (zerop (window-resizable (selected-window) delta t)) nil
            (window-resize (selected-window) delta t))
          delta))

      (global-set-key (kbd "C-x w") 'k20e/fit-window-to-buffer-horizontally)
    #+END_SRC

** Windows

   #+BEGIN_SRC emacs-lisp
     (require 'ivy)

     (defun split-window-right-and-balance-and-go-there-and-switch-buffer (&optional arg)
       "Optional argument ARG Prefix argument will switch buffer using ivy."
       (interactive "P")
       (split-window-right)
       (balance-windows-area)
       (windmove-right)
       (if arg
           (ivy-switch-buffer)
         (switch-to-buffer nil)))

     (defun delete-window-and-balance ()
       "Balance windows after deleting."
       (interactive)
       (delete-window)
       (balance-windows-area))
   #+END_SRC

   Bind window functions:

   #+BEGIN_SRC emacs-lisp
     (global-set-key (kbd "C-x 0") 'delete-window-and-balance)
     (global-set-key (kbd "C-x 3") 'split-window-right-and-balance-and-go-there-and-switch-buffer)
   #+END_SRC

** Networking

   #+BEGIN_SRC emacs-lisp
     (require 'net-utils)
     (require 'tramp)

     (defun k20e/known-hosts ()
       "Get a host name from ~./ssh/known_hosts file."
       (completing-read "host: "
                        (let ((value))
                          (dolist (elt (tramp-parse-shosts "~/.ssh/known_hosts") value)
                            (if elt (setq value (cons (cadr elt) value)))))))

     (defun k20e/host-ip ()
       "Insert the current IP of a host using `dns-lookup-program'.
     Similar to but simpler than `dns-lookup-host'."
       (interactive)
       (let ((host (k20e/known-hosts)))
         (insert (car (last (split-string (shell-command-to-string
                                           (concat dns-lookup-program " " host))))))))
   #+END_SRC

** Lunar üåô

   #+BEGIN_SRC emacs-lisp
     (require 'calendar)
     (require 'lunar)

     (defun k20e/full-moons-info ()
       "Get a list of upcoming full moons info beginning with the current month.
     See `lunar-phase-list' and `lunar-phase-name'."
       (let* ((current-date (calendar-current-date))
              (current-month (car current-date))
              (current-year (car (last current-date)))
              (full-moon-phase-index 2)
              (k20e/full-moons-info '()))
         (dolist (phase (lunar-phase-list current-month current-year))
           (if (= (car (last phase)) full-moon-phase-index)
               (setq k20e/full-moons-info (cons phase k20e/full-moons-info))))
         (reverse k20e/full-moons-info)))

     (defun k20e/full-moons ()
       "Display upcoming full moons beginning with the current month."
       (interactive)
       (with-output-to-temp-buffer "*full-moons*"
         (princ
          (mapconcat
           #'(lambda (x)
               (format "%s %s" (calendar-date-string (car x)) (car (cdr x))))
           (k20e/full-moons-info)
           "\n"))))
   #+END_SRC

* auto-fill

  When to turn on auto-fill and set fill-column to a reasonable value.  This
  would probably be better dealt with by a data structure that maps mode hooks
  to fill-column values.

  #+BEGIN_SRC emacs-lisp
    (defun k20e/auto-fill-mode-hook ()
      (setq fill-column 78))

    (add-hook 'auto-fill-mode 'k20e/auto-fill-mode-hook)
  #+END_SRC

* auto-save

  Disable =auto-save=.

  #+BEGIN_SRC emacs-lisp
    (setq auto-save-default nil
          auto-save-timeout 0)
  #+END_SRC

* avy

  #+BEGIN_SRC emacs-lisp
    (require 'avy)

    (global-set-key (kbd "s-g") 'avy-goto-char-timer)

    (setq-default
     avy-keys '(?a ?o ?e ?u ?i ?d ?h ?t ?n)
     avy-all-windows nil
     avy-style 'at-full)
  #+END_SRC

* buffer-move

  Move the current buffer up/down/left/right easily.

  #+BEGIN_SRC emacs-lisp
    (require 'buffer-move)

    (global-set-key (kbd "<M-S-up>") 'buf-move-up)
    (global-set-key (kbd "<M-S-down>") 'buf-move-down)
    (global-set-key (kbd "<M-S-left>") 'buf-move-left)
    (global-set-key (kbd "<M-S-right>") 'buf-move-right)
  #+END_SRC

* C

  #+BEGIN_SRC emacs-lisp
    (require 'flycheck)

    (defun k20e/c-mode-hook ()
      (setq flycheck-checker 'c/c++-gcc
            flycheck-gcc-pedantic-errors t)
      (setq-default flycheck-c/c++-gcc-executable "gcc-7")
      (flycheck-mode 1))

    (add-hook 'c-mode-hook 'k20e/c-mode-hook)
  #+END_SRC

* clock-face

  This is a [[https://github.com/krismolendyke/clock-face.el][ridiculous package]] that I wrote to insert a Unicode clock
  face character for the nearest current half-hour.  üïô

  #+BEGIN_SRC emacs-lisp
    (require 'clock-face)
  #+END_SRC

* compilation-mode

  #+BEGIN_SRC emacs-lisp
    (defun k20e/compilation-mode-hook ()
      (set-face-foreground 'compilation-error "tomato1"))

    (add-hook 'compilation-mode-hook 'k20e/compilation-mode-hook)
  #+END_SRC

  Functions to execute after compilation has finished:

  #+BEGIN_SRC emacs-lisp
    (require 'hl-line)
    (require 'subr-x)

    (defun k20e/compilation-finish-function-delay-delete (buf result)
      "Delete and bury BUF after short delay.
    Do so only if compilation is successful."
      (if (string= (string-trim result) "finished")
          (run-with-timer
           1.0 nil
           (lambda (buf)
             (with-current-buffer buf
               (delete-window)
               (bury-buffer)))
           buf)))

    (defun k20e/compilation-finish-function-select-window (buf result)
      "Switch to the compilation buffer BUF.
    When compilation completes, regardless of result."
      (let ((win (get-buffer-window buf)))
        (select-window (get-buffer-window buf))
        (goto-char (point-max))
        (forward-line -1)
        (hl-line-mode)))
  #+END_SRC

* Counsel
  :PROPERTIES:
  :CUSTOM_ID: counsel
  :END:

  #+BEGIN_SRC emacs-lisp
    (require 'counsel)

    (global-set-key (kbd "M-x") 'counsel-M-x)

    (global-set-key (kbd "C-x C-f") 'counsel-find-file)
    (global-set-key (kbd "<f1> f") 'counsel-describe-function)
    (global-set-key (kbd "<f1> v") 'counsel-describe-variable)
    (global-set-key (kbd "<f1> l") 'counsel-find-library)
    (global-set-key (kbd "<f1> s") 'counsel-info-lookup-symbol)

    (global-set-key (kbd "C-x 8 RET") 'counsel-unicode-char)

    (define-key read-expression-map (kbd "C-r") 'counsel-expression-history)
  #+END_SRC

** Find File Ignore

   #+BEGIN_SRC emacs-lisp
     (require 'regexp-opt)

     (setq-default counsel-find-file-ignore-regexp (regexp-opt '(".pyc")))
   #+END_SRC

* dired

  #+BEGIN_SRC emacs-lisp
    (require 'dired-x)
    (require 'autorevert)

    (defun k20e/dired-load-hook ()
      (load "dired-x"))

    (add-hook 'dired-load-hook 'k20e/dired-load-hook)

    (defun k20e/dired-mode-hook ()
      (auto-revert-mode 1)
      (setq auto-revert-verbose nil)
      (set-face-foreground 'dired-flagged "tomato1")
      (set-face-attribute 'dired-flagged nil :strike-through t))

    (add-hook 'dired-mode-hook 'k20e/dired-mode-hook)
  #+END_SRC

* electric-pair-mode

  #+BEGIN_SRC emacs-lisp
    (require 'elec-pair)

    (electric-pair-mode t)
  #+END_SRC

* emacs-lisp-mode

  #+BEGIN_SRC emacs-lisp
    (defun k20e/emacs-lisp-mode-hook ()
      (eldoc-mode))

    (add-hook 'emacs-lisp-mode-hook 'k20e/emacs-lisp-mode-hook)
  #+END_SRC

* ert

  Emacs Lisp [[http://en.wikipedia.org/wiki/Unit_testing][unit testing]]!

  #+BEGIN_SRC emacs-lisp
    (require 'ert)

    (defun k20e/ert ()
      "Run all the tests in the universe!"
      (interactive)
      (ert t))

    (define-key emacs-lisp-mode-map (kbd "H-t") 'k20e/ert)
  #+END_SRC

* expand-region

  #+BEGIN_SRC emacs-lisp
    (require 'expand-region)

    (global-set-key (kbd "C-M-SPC") 'er/expand-region)
  #+END_SRC

* find-file-in-project

  #+BEGIN_SRC emacs-lisp
    (require 'find-file-in-project)

    (setq-default ffip-limit 8192
                  ffip-find-options "-not -regex \".*/build.*\"" ; TODO ignore .tox
                  ffip-full-paths t
                  ffip-patterns (list "*.el"
                                      "*.html"
                                      "*.js"
                                      "*.json"
                                      "*.go"
                                      "*.md"
                                      "*.org"
                                      "*.py"
                                      "*.sh"
                                      "*.txt"
                                      "*.yaml"
                                      "*.yml"
                                      "Dockerfile"
                                      "Makefile")
                  ffip-prune-patterns (list ".git" "build"))

    (global-set-key (kbd "C-x o") 'find-file-in-project)
  #+END_SRC

* flycheck
  :PROPERTIES:
  :CUSTOM_ID: flycheck
  :END:

  #+BEGIN_SRC emacs-lisp
    (require 'flycheck)

    (setq-default flycheck-pylintrc "pylintrc"
                  flycheck-check-syntax-automatically '(mode-enabled save))
  #+END_SRC

* flyspell

  Setup =ispell= to use [[#install-aspell][=aspell=]]:

  #+BEGIN_SRC emacs-lisp
    (setq-default ispell-program-name "aspell"
                  ispell-extra-args (list "--sug-mode=ultra"))
  #+END_SRC

  Then setup =flyspell= itself.  It requires  =ispell=.

  #+BEGIN_SRC emacs-lisp
    (require 'flyspell)

    ;; When to turn on flyspell-mode.
    (dolist (hook '(text-mode-hook))
      (add-hook hook 'turn-on-flyspell))

    ;; When to turn on flyspell-prog-mode for comments and strings in source.
    ;; (dolist (hook '(emacs-lisp-mode-hook
    ;;                 lisp-mode-hook))
    ;;   (add-hook hook #'(lambda () (flyspell-prog-mode))))

    ;; Do not emit to *Messages*.
    (setq flyspell-issue-message-flag nil
          flyspell-issue-welcome-flag nil)
  #+END_SRC

** Install [[http://hunspell.sourceforge.net/][=aspell=]]
   :PROPERTIES:
   :CUSTOM_ID: install-aspell
   :END:

   Install =aspell= via Homebrew:

   #+BEGIN_SRC sh
     brew install aspell --with-lang-en
   #+END_SRC

* Fonts

  I purchased the excellent font family [[https://www.fsd.it/shop/fonts/pragmatapro/][PragmataPro]] and use it
  exclusively now, rather than toggling between various proportional
  and fixed width fonts as I read and edit.

  The =add-pragmatapro-prettify-symbols-alist= function below is from
  a file I load from the =elisp= directory at initialization.  That
  file should be kept in sync with the version of the PragmataPro font
  that is currently installed.  The font author releases updates here
  https://github.com/fabrizioschiavi/pragmatapro/tree/master/emacs_snippets.

  #+BEGIN_SRC emacs-lisp
    (require 'prog-mode)

    (defun k20e/prog-mode-hook ()
      (prettify-hook))

    (add-hook 'prog-mode-hook 'k20e/prog-mode-hook)
  #+END_SRC

  #+BEGIN_SRC emacs-lisp
    (global-prettify-symbols-mode 1)

    (defvar k20e/font-list '(("PragmataPro-Liga" . 14)
                             ("PragmataPro" . 14))
      "Ordered list of preferred fonts and sizes.")

    (defun k20e/font--set (font-alist)
      "Set the font family and size to the given font alist of the
    format (family . point)."
      (let ((font (replace-regexp-in-string "-" " " (car font-alist)))
            (height (* 10 (cdr font-alist))))
        (set-frame-font font)
        (set-face-attribute 'default nil :height height)))

    (defun k20e/font-set-from-list (l)
      "Set the font to first available font alist in the given list."
      (if (null l) nil
        (k20e/font--set (car l))
        (if (string= (replace-regexp-in-string "-" " "(caar l))
                     (face-attribute 'default :family (selected-frame)))
            (caar l)
          (k20e/font-set-from-list (cdr l)))))

    (defun k20e/font-set ()
      "Set a font from the `k20e/font-list'."
      (interactive)
      (let ((ignore-case completion-ignore-case))
        (unwind-protect
            (progn
              (setq completion-ignore-case t)
              (let ((font (completing-read "Font: " k20e/font-list)))
                (k20e/font--set (assoc font k20e/font-list))))
          (setq completion-ignore-case ignore-case))))

    (k20e/font-set-from-list k20e/font-list)
  #+END_SRC

** Unicode

   [[http://users.teilar.gr/~g1951d/][Symbola]] is a nice font for displaying Unicode characters üç∫üëç.

    #+BEGIN_SRC emacs-lisp
      (when (member "Symbola" (font-family-list))
        (set-fontset-font t 'unicode "Symbola" nil 'prepend))
    #+END_SRC

* font-awesome

  This is a [[https://github.com/krismolendyke/font-awesome.el][na√Øve package]] that I wrote to help insert [[http://fortawesome.github.io/Font-Awesome/][Font Awesome]]
  icons into buffers.

  #+BEGIN_SRC emacs-lisp
    (require 'font-awesome)
  #+END_SRC

* geiser

  #+BEGIN_SRC emacs-lisp
    (setq-default geiser-active-implementations '(racket chicken))
  #+END_SRC

  Default to [[https://racket-lang.org/][Racket]].

  #+BEGIN_SRC emacs-lisp
    (setq-default geiser-default-implementation 'racket)
  #+END_SRC

* git

  #+BEGIN_SRC emacs-lisp
    (require 'counsel)
    (require 'gitconfig-mode)
    (require 'gitignore-mode)

    (autoload 'git-blame-mode "git-blame"
      "Minor mode for incremental blame for Git." t)

    (global-set-key (kbd "C-c g") 'counsel-git)
    (global-set-key (kbd "C-c j") 'counsel-git-grep)
    (global-set-key (kbd "C-x v b") 'git-blame-mode)

  #+END_SRC

** GitHub =.gitignore=

   A simple function to insert starter =.gitignore= file contents from
   the [[https://github.com/github/gitignore][github/gitignore]] repository.

   #+BEGIN_SRC emacs-lisp
     (require 'url)

     (defun k20e/gh--gitignore-url (language)
       "Get GitHub .gitignore URL for LANGUAGE."
       (format "https://raw.githubusercontent.com/github/gitignore/master/%s.gitignore"
               (capitalize language)))

     (defun k20e/gh--gitignore-get-region (response-buffer)
       "Get GitHub .gitignore response body bounds.
     Argument RESPONSE-BUFFER HTTP GET response."
       (with-current-buffer response-buffer
         (goto-char (point-min))
         (let ((start (1+ (search-forward-regexp "^$")))
               (end (point-max)))
           (list start end))))

     (defun k20e/gh-gitignore-insert (language)
       "Insert Github .gitignore for LANGUAGE."
       (interactive "sLanguage: ")
       (let* ((response-buffer (url-retrieve-synchronously
                                (k20e/gh--gitignore-url language) t))
              (gitignore-region (k20e/gh--gitignore-get-region response-buffer)))
         (insert-buffer-substring-no-properties
          response-buffer (car gitignore-region) (cadr gitignore-region))))
   #+END_SRC

* go-mode

  Install commands:

  #+BEGIN_SRC sh
    pushd "${TMPDIR}" && \
        GO111MODULE=on go get golang.org/x/tools/gopls@latest; \
        GO111MODULE=on go get honnef.co/go/tools/cmd/staticcheck; \
        popd
  #+END_SRC

  Switched from =staticcheck= to =golangci-lint= but its install
  doesn't recommend =go get -u=.

  #+BEGIN_SRC emacs-lisp
    (use-package company
      :ensure t
      :bind
      (:map company-active-map
            ("C-n" . 'company-select-next)
            ("C-p" . 'company-select-previous)
            ("C-h" . 'delete-backward-char))
      :config
      (setq company-idle-delay nil
            company-show-numbers t
            company-tooltip-align-annotations t))

    (use-package flycheck
      :ensure t)

    (use-package lsp-mode
      :ensure t
      :hook ((go-mode . lsp-deferred)
             (before-save . (lambda () (when (eq major-mode 'go-mode)
                                         (lsp-format-buffer)
                                         (lsp-organize-imports)))))
      :bind
      (:map go-mode-map
            ("M-Q" . lsp-format-buffer))
      :config
      ;; Officially supported settings get first class vars, experimental
      ;; settings are "custom"
      ;; https://github.com/golang/tools/blob/master/gopls/doc/emacs.md#gopls-configuration

      ;; Officially supported settings
      ;; https://github.com/golang/tools/blob/master/gopls/doc/settings.md#officially-supported
      (setq-default lsp-go-link-target "pkg.go.dev"
                    lsp-go-use-placeholders t
                    lsp-eldoc-render-all nil
                    lsp-signature-doc-lines 20)

      ;; Experimental settings
      ;; https://github.com/golang/tools/blob/master/gopls/doc/settings.md#experimental
      (lsp-register-custom-settings
       '(("gopls.completeUnimported" t t)
         ("gopls.staticcheck" t t)))

      (set-face-attribute 'lsp-face-highlight-textual nil :underline t))

    (use-package go-mode
      :ensure t
      :bind
      (:map go-mode-map
            ("C-c C-t" . 'k20e/toggle-test-buffer)
            ("C-." . 'company-indent-or-complete-common))
      :preface
      (defun k20e/go-mode ()
        (message "hey, go-mode!"))
      :hook ((go-mode . k20e/go-mode)
             (go-mode . (lambda ()
                          (let ((gopath (string-trim (shell-command-to-string (string-join `(,(executable-find "go") "env" "GOPATH") " "))))
                                (goroot (string-trim (shell-command-to-string (string-join `(,(executable-find "go") "env" "GOROOT") " ")))))
                            (setenv "GOPATH" gopath)
                            (setenv "GOROOT" goroot)
                            (set (make-local-variable 'compile-command) "go build -v ")
                            (setq fill-column 118
                                  indent-tabs-mode t
                                  tab-width 4)
                            (set-face-attribute 'button nil :box nil))))))
  #+END_SRC

** [0/3] =TODO=

   - [ ] Steal some of these https://github.com/dominikh/yasnippet-go
   - [ ] https://github.com/nlamirault/gotest.el
   - [ ] https://github.com/alecthomas/gometalinter

* highlight-indent-guides

  #+BEGIN_SRC emacs-lisp
    (require 'highlight-indent-guides)

    (setq highlight-indent-guides-method 'character
          highlight-indent-guides-responsive 'stack)
  #+END_SRC

* highlight-parentheses

  #+BEGIN_SRC emacs-lisp
    (require 'highlight-parentheses)

    (dolist (hook '(emacs-lisp-mode-hook
                    lisp-mode-hook))
      (add-hook hook #'(lambda ()
                         (highlight-parentheses-mode))))
  #+END_SRC

* hyperspec

  #+BEGIN_SRC emacs-lisp
    ;; Set HyperSpec root in Google Drive.
    (defvar common-lisp-hyperspec-root
      (format "file://%s/"
              (expand-file-name "Documents/HyperSpec" k20e/google-drive-directory)))
  #+END_SRC

* ibuffer
  :PROPERTIES:
  :CUSTOM_ID: ibuffer
  :END:

  #+BEGIN_SRC emacs-lisp
    (require 'face-remap)

    (defalias 'list-buffers 'ibuffer)

    (defvar ibuffer-formats
          '((mark " "
                  (modified)
                  " "
                  (name 40 40 :right :elide)
                  " "
                  (filename-and-process))
            (mark " "
                  (filename-and-process 70 70 :left :elide)
                  " "
                  name)))
  #+END_SRC

* IELM

  #+BEGIN_SRC emacs-lisp
    (require 'eldoc)
    (require 'paredit)

    (defun k20e/ielm-hook ()
      (eldoc-mode)
      (paredit-mode 1))

    (add-hook 'ielm-mode-hook 'k20e/ielm-hook)
  #+END_SRC

* imenu

  Re-scan the buffer for new menu items automatically.

  #+BEGIN_SRC emacs-lisp
    (setq-default imenu-auto-rescan t)
  #+END_SRC

* I'm Feeling Lucky

  This is [[https://github.com/krismolendyke/im-feeling-lucky.el][my Google search]] module.

  #+BEGIN_SRC emacs-lisp
    (require 'im-feeling-lucky)

    (global-set-key (kbd "H-l") 'ifl-region-or-query)
  #+END_SRC

* Ivy

  #+BEGIN_SRC emacs-lisp
    (require 'ivy)

    (ivy-mode 1)

    (setq ivy-extra-directories nil
          ivy-use-virtual-buffers t
          ivy-wrap t)

    (global-set-key (kbd "C-c C-r") 'ivy-resume)
    (global-set-key (kbd "C-x b") 'ivy-switch-buffer)
  #+END_SRC

  #+BEGIN_QUOTE
  =M-j= (=ivy-yank-word=)

    Inserts the sub-word at point into the minibuffer.

    This is similar to =C-s C-w= with =isearch=. Ivy reserves =C-w=
    for =kill-region=. -- [[http://oremacs.com/swiper/#minibuffer-key-bindings][Ivy minibuffer key bindings]]
  #+END_QUOTE

  =isearch-forward-word= was mapped to =w= and my muscle memory
  requires this:

  #+BEGIN_SRC emacs-lisp
    (define-key ivy-minibuffer-map (kbd "C-w") 'ivy-yank-word)
  #+END_SRC

  #+BEGIN_QUOTE
  =C-j= (=ivy-alt-done=)

    When completing file names, selects the current directory
    candidate and starts a new completion session there. Otherwise, it
    is the same as =ivy-done=.
  #+END_QUOTE

  Having to type =C-j= to go into a directory when finding files is
  maddening and I almost never used =dired=.  Swap =RET= to go into
  directories and =C-j= to open current directory in =dired=:

  #+BEGIN_SRC emacs-lisp
    (define-key ivy-minibuffer-map (kbd "RET") 'ivy-alt-done)
    (define-key ivy-minibuffer-map (kbd "C-j") 'ivy-done)
  #+END_SRC

* js-mode

  #+BEGIN_SRC emacs-lisp
    (require 'flycheck)
    (require 'json)

    (defun k20e/js-mode-hook ()
      (setq-default flycheck-gjslintrc "gjslintrc")
      (flycheck-mode 1))

    (add-hook 'js-mode-hook 'k20e/js-mode-hook)

    (add-to-list 'auto-mode-alist '("\\.json\\'" . js-mode))
    (add-to-list 'auto-mode-alist '("Pipfile.lock\\'" . js-mode))
  #+END_SRC

* keyfreq

  #+BEGIN_SRC emacs-lisp
    (require 'keyfreq)

    (setq keyfreq-file (expand-file-name ".emacs-keyfreq" k20e/google-drive-directory)
          keyfreq-file-lock (expand-file-name ".emacs-keyfreq-lock" k20e/google-drive-directory))

    (keyfreq-mode 1)
    (keyfreq-autosave-mode 1)
  #+END_SRC

* LaTeX

  #+BEGIN_SRC emacs-lisp
    (require 'tex-mode)

    (define-key latex-mode-map (kbd "C-j") 'join-line)
  #+END_SRC

* lockfiles

  [[http://stackoverflow.com/questions/5738170/why-does-emacs-create-temporary-symbolic-links-for-modified-files][Avoid creating temporary symbolic links]] and disturbing working
  directory state at the expense of avoiding editing collisions that I
  do not ever anticipate.

  #+BEGIN_SRC emacs-lisp
    (setq create-lockfiles nil)
  #+END_SRC

* man

  Setting a width avoids a possibly (likely) poorly chosen automatic
  width.

  #+BEGIN_SRC emacs-lisp
    (setq-default Man-width 80)
  #+END_SRC

* markdown-mode

  #+BEGIN_SRC emacs-lisp
    (require 'markdown-mode)

    (defun k20e/markdown-mode-hook ()
      (add-pragmatapro-prettify-symbols-alist)
      (prettify-symbols-mode 1)
      (setq markdown-open-command "open"
            fill-column 118))

    (add-hook 'markdown-mode-hook 'k20e/markdown-mode-hook)
  #+END_SRC

* Minibuffer

  Scale up the minibuffer text size and limit how tall it can get.

  #+BEGIN_SRC emacs-lisp
    (defun k20e/minibuffer-setup-hook ()
      "Bump up minibuffer text size and height."
      (text-scale-set 3)
      (setq max-mini-window-height 20))

    (add-hook 'minibuffer-setup-hook 'k20e/minibuffer-setup-hook)
  #+END_SRC

  Set =enable-recursive-minibufers= to =t= to allow minibuffers
  /within/ minibuffers.  A good use-case of this feature is described
  in [[http://www.masteringemacs.org/articles/2011/10/19/executing-shell-commands-emacs/][Executing Shell Commands in Emacs]].

  #+BEGIN_SRC emacs-lisp
    (setq enable-recursive-minibuffers t)
  #+END_SRC

** Eval expression minibuffer

   Enable =eldoc= in the modeline.

   #+BEGIN_SRC emacs-lisp
     (require 'eldoc)

     (defun k20e/eval-expression-minibuffer-setup-hook ()
       (eldoc-mode 1))

     (add-hook 'eval-expression-minibuffer-setup-hook
               'k20e/eval-expression-minibuffer-setup-hook)
   #+END_SRC

* Mouse

  Scroll one line at a time:

  #+BEGIN_SRC emacs-lisp
    (setq-default mouse-wheel-scroll-amount '(1 ((shift) . 1) ((meta)) ((control) . text-scale)))
  #+END_SRC

* multi-term
  :PROPERTIES:
  :CUSTOM_ID: multi-term
  :END:

  Together with [[#term][term]] this sets up my terminal environment within Emacs.

  =multi-term= adds a nice shortcut for flipping between only terminal
  buffers.  It also lets me fix a big annoyance by binding =M-h= to
  =backward-kill-word= easily.

  #+BEGIN_SRC emacs-lisp
    (require 'term)

    (defun k20e/term-toggle-mode ()
      "Toggle between `term-line-mode' and `term-char-mode'."
      (interactive)
      (if (term-in-char-mode)
          (term-line-mode)
        (term-char-mode)))
  #+END_SRC

  #+BEGIN_SRC emacs-lisp
    (require 'face-remap)
    (require 'multi-term)

    (defcustom term-bind-key-alist
        '(("C-c C-c" . term-interrupt-subjob)
          ("C-h" . term-send-backspace)
          ("C-c C-j" . k20e/term-toggle-mode)
          ("C-c C-k" . k20e/term-toggle-mode)
          ("C-p" . term-send-up)
          ("C-n" . term-send-down)
          ("C-r" . term-send-reverse-search-history)
          ("C-m" . term-send-raw)
          ("C-y" . term-send-raw)
          ("C-z" . term-stop-subjob)
          ("M-f" . term-send-forward-word)
          ("M-b" . term-send-backward-word)
          ("M-p" . previous-line)
          ("M-n" . next-line)
          ("M-d" . term-send-forward-kill-word)
          ("M-h" . term-send-backward-kill-word)
          ("M-r" . isearch-backward)
          ("M-s" . isearch-forward)
          ("M-." . completion-at-point)
          ("M-]" . multi-term-next)
          ("M-[" . multi-term-prev))
        "Custom key bindings for `multi-term'."
        :type 'alist
        :group 'multi-term)

    (defun k20e/term-mode-hook ()
      "Re-evaluate my custom key bindings.
     Make sure a fixed width font is set. Disable highlight line."
      (custom-reevaluate-setting 'term-bind-key-alist)
      (setq-local global-hl-line-mode nil))

    (add-hook 'term-mode-hook 'k20e/term-mode-hook)
  #+END_SRC

  =k20e/multi-term-hook= is necessary to re-evaluate my custom key bindings
  after =multi-term= is loaded.  Otherwise it overrides my bindings with its
  bindings whenever I open a new terminal.

** Global Key Bindings

   #+BEGIN_SRC emacs-lisp
     (global-set-key (kbd "<f2>") 'multi-term)
     (global-set-key (kbd "<f11>") 'multi-term-next)
   #+END_SRC

* multiple-cursors

  #+BEGIN_SRC emacs-lisp
    (require 'multiple-cursors)
  #+END_SRC

  #+BEGIN_SRC emacs-lisp
    (defun k20e/mark-next (extended)
      "Wrap multiple-cursors mark-more/next.
    Call `mc/mark-next-like-this' without a prefix argument.
    Argument EXTENDED Prefix argument to call function `mc/mark-more-like-this-extended'."
      (interactive "P")
      (if extended
          (call-interactively 'mc/mark-more-like-this-extended)
        (call-interactively 'mc/mark-next-like-this)))

    (defun k20e/mark-previous (extended)
      "Wrap multiple-cursors mark-more/previous.
    Call `mc/mark-previous-like-this' without a prefix argument.
    Argument EXTENDED Prefix argument to call function `mc/mark-more-like-this-extended'."
      (interactive "P")
      (if extended
          (call-interactively 'mc/mark-more-like-this-extended)
        (call-interactively 'mc/mark-previous-like-this)))
  #+END_SRC

  Setup key bindings:

  #+BEGIN_SRC emacs-lisp
    (global-set-key (kbd "M-L") 'mc/edit-lines)
    (global-set-key (kbd "C-M-.") 'k20e/mark-next)
    (global-set-key (kbd "C-M-,") 'k20e/mark-previous)
    (global-set-key (kbd "C-M-<return>") 'mc/mark-all-like-this)
  #+END_SRC

  Keep preferences sync'd across machines.

  #+BEGIN_SRC emacs-lisp
    (setq mc/list-file (expand-file-name ".mc-lists.el" k20e/google-drive-directory))
  #+END_SRC

* Open Source Licenses

  #+BEGIN_SRC emacs-lisp
    (defun k20e/insert-mit-license ()
      "Insert MIT license file contents.
    Populate the current year and user name."
      (interactive)
      (with-current-buffer (get-buffer-create "LICENSE.txt")
        (insert (format "The MIT License (MIT)

    Copyright (c) %s %s

    Permission is hereby granted, free of charge, to any person obtaining a copy
    of this software and associated documentation files (the \"Software\"), to deal
    in the Software without restriction, including without limitation the rights
    to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
    copies of the Software, and to permit persons to whom the Software is
    furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be included in
    all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED \"AS IS\", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
    IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
    FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
    AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
    LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
    OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
    THE SOFTWARE.
    " (format-time-string "%Y") (user-full-name)))))
  #+END_SRC

* org-mode

  #+BEGIN_SRC emacs-lisp
    (require 'org)

    (defun k20e/org-mode-hook ()
      (auto-fill-mode 1)
      (visual-line-mode 0)

      (add-pragmatapro-prettify-symbols-alist)
      (prettify-symbols-mode 1)

      (setq org-pretty-entities t           ; Display entities as UTF-8 characters.
            truncate-lines nil
            org-ellipsis "‚Ä¶"
            org-fontify-quote-and-verse-blocks t
            org-fontify-whole-heading-line t)
      (setq-local global-hl-line-mode nil))

    (add-hook 'org-mode-hook 'k20e/org-mode-hook)

    ;; Set the org directory.
    (setq org-directory (expand-file-name "org" k20e/google-drive-directory))

    ;; Speeeeeeeeeed!  Move to very beginning of a headline and press "?"
    (setq org-use-speed-commands t)

    ;; "Special" `C-a' and `C-e' movement in headlines.
    (setq org-special-ctrl-a/e t)

    ;; Use completion in the current buffer for movement.
    (setq org-goto-interface 'outline-path-completion)

    ;; org-capture.
    (setq org-default-notes-file (expand-file-name "notes.org" org-directory))

    ;; Global key binding to make storing links to files easier.
    (global-set-key (kbd "C-c l") 'org-store-link)

    ;; Use Ivy for completion
    (setq org-outline-path-complete-in-steps nil)

    (setq org-log-redeadline 'time
          org-log-reschedule 'time)
  #+END_SRC

** UPGRAYEDD

   #+BEGIN_SRC sh
     cd ~/.emacs.d/site-lisp/org-mode
     g fetch origin --tags
     g co -b release_9.1.2 release_9.1.2
     make clean
     make
     cd ~/.emacs.d
     g add site-lisp/org-mode
     g commit -m "UPGRAYEDD org-mode"
   #+END_SRC

** Inline Images

   Try to get the width of images displayed inline from a =#+ATTR.*=
   keyword, e.g., =#+ATTR_HTML: :width 800px=, fall back to original
   image width if no attribute keyword is found:

   #+BEGIN_SRC emacs-lisp
     (setq org-image-actual-width nil)
   #+END_SRC

** Key Bindings

   #+BEGIN_SRC emacs-lisp
     (require 'ivy)

     (global-set-key (kbd "C-c a") 'org-agenda)
     (global-set-key (kbd "C-x c") 'org-switchb)
     (global-set-key (kbd "<f12>") 'org-agenda-list)

     (define-key org-mode-map (kbd "<return>") 'org-return-indent)
     (define-key org-mode-map (kbd "M-<return>") 'org-meta-return)
     (define-key org-mode-map (kbd "C-c C-r") 'ivy-resume)
     (define-key org-mode-map (kbd "C-j") 'join-line)
     (define-key org-mode-map (kbd "C-m") 'org-return-indent)
     (define-key org-mode-map (kbd "H-<tab>") 'pcomplete)
     (define-key org-mode-map (kbd "M-h") 'backward-kill-word)
   #+END_SRC

** Export

   - https://github.com/dakrone/ox-tufte
   - https://github.com/tsdye/tufte-org-mode

   Most non-interactive export settings are defined in [[https://github.com/krismolendyke/.emacs.d/blob/master/elisp/k20e-org-html-export.el][a file loaded
   during initialization]].  Those settings are defined during
   initialization time to support a fast batch process for exporting
   /this/ document to HTML in a Git =post-commit= hook.

   #+BEGIN_SRC emacs-lisp
     (require 'k20e-org-html-export)
   #+END_SRC

   Interactive customization can be done here.

   #+BEGIN_SRC emacs-lisp
     (require 'ox-publish)

     ;; Enable "expert" export interface.
     (setq org-export-dispatch-use-expert-ui t)
   #+END_SRC

*** Options

    A message in =*Messages*= like:

    #+BEGIN_EXAMPLE
      user-error: Unable to resolve link: nil
    #+END_EXAMPLE

    indicates that a link somewhere is malformed.  Adding the option:

    #+BEGIN_SRC org
      ,#+OPTIONS: broken-links:mark
    #+END_SRC

    and exporting will insert =BROKEN= into the HTML document.
    Searching for that token makes finding the offending broken link
    much easier.  Keeping this option set all the time would let
    broken links slip through the export process undetected.

*** Backends

    #+BEGIN_SRC emacs-lisp
      (require 'ox-md)

      (add-to-list 'org-export-backends 'md)
    #+END_SRC

    #+BEGIN_SRC emacs-lisp
      (require 'ox-tufte)
    #+END_SRC

** Publish

   #+BEGIN_SRC emacs-lisp
     (setq org-publish-project-alist
           `(("k20e.com-org-files"
              :base-directory ,(expand-file-name "source" (expand-file-name "k20e.com" k20e/google-drive-directory))
              :base-extension "org"
              :recursive t
              :exclude "ga.org\\|level-0.org\\|todo.org\\|.DS_Store"
              :publishing-directory ,(expand-file-name "published" (expand-file-name "k20e.com" k20e/google-drive-directory))
              :publishing-function org-html-publish-to-html
              :with-planning t)
             ("k20e.com-static-files"
              :base-directory ,(expand-file-name "source" (expand-file-name "k20e.com" k20e/google-drive-directory))
              :base-extension "jpg\\|png\\|ico"
              :recursive t
              :publishing-directory ,(expand-file-name "published" (expand-file-name "k20e.com" k20e/google-drive-directory))
              :publishing-function org-publish-attachment)
             ("k20e.com"
              :components ("k20e.com-org-files" "k20e.com-static-files"))
             ("work-org-files"
              :base-directory ,(expand-file-name "work" org-directory)
              :base-extension "org"
              :publishing-directory ,(expand-file-name "published" (expand-file-name "work" org-directory))
              :publishing-function org-html-publish-to-html
              :with-planning t)
             ("work-static-files"
              :base-directory ,(expand-file-name "work" org-directory)
              :base-extension "pdf\\|csv\\|sql\\|png"
              :publishing-directory ,(expand-file-name "published" (expand-file-name "work" org-directory))
              :publishing-function org-publish-attachment)
             ("work"
              :components ("work-org-files" "work-static-files"))
             ("house-org-files"
              :base-directory ,(expand-file-name "house" org-directory)
              :base-extension "org"
              :recursive t
              :publishing-directory ,(expand-file-name "published" (expand-file-name "house" org-directory))
              :publishing-function org-html-publish-to-html
              :with-planning t)
             ("house-static-files"
              :base-directory ,(expand-file-name "house" org-directory)
              :base-extension "pdf\\|csv\\|png\\|xls\\|doc"
              :recursive t
              :publishing-directory ,(expand-file-name "published" (expand-file-name "house" org-directory))
              :publishing-function org-publish-attachment)
             ("house"
              :components ("house-org-files" "house-static-files"))))
   #+END_SRC

** Babel

   Define [[http://orgmode.org/worg/org-contrib/babel/languages.html][which languages]] =org-babel= should support.

   #+BEGIN_SRC emacs-lisp
     (defvar k20e/org-babel-load-languages
       '((ditaa . t)
         (emacs-lisp . t)
         (js . t)
         (org . t)
         (python . t)
         (scheme . t)
         (shell . t)
         (sql . t))
       "Languages to evaluate in `org-mode'.")

     (org-babel-do-load-languages 'org-babel-load-languages
                                  k20e/org-babel-load-languages)
   #+END_SRC

   Disable interactive prompt for executing code blocks.  This is
   dangerous but I never execute any org files that I didn't author.

   #+BEGIN_SRC emacs-lisp
     (setq org-confirm-babel-evaluate nil)
   #+END_SRC

** TODO Items

   Automatically insert a timestamp when a task is marked =DONE=.

   #+BEGIN_SRC emacs-lisp
     (setq org-log-done t)
   #+END_SRC

   Custom keywords and faces.

   #+BEGIN_SRC emacs-lisp
     (setq org-todo-keywords '((sequence
                                "TODO(t)"
                                "STARTED(s/!)"
                                "|"
                                "DONE(d!)"
                                "CANCELED(c@)"))
           org-todo-keyword-faces '(("TODO" . org-todo)
                                    ("STARTED" . org-code)
                                    ("CANCELED" . org-ellipsis)
                                    ("DONE" . org-done)))
   #+END_SRC

** Agenda

   #+BEGIN_SRC emacs-lisp
     (require 'face-remap)
     (require 'org)
     (require 'org-agenda)
     (require 'winner)

     (defun k20e/org-agenda-mode-hook ()
       (define-key org-agenda-mode-map (kbd "q")
         (lambda (x)
           (interactive "p")
           (winner-undo)
           (kill-buffer "*Org Agenda*")))
       (delete-other-windows)
       (text-scale-set 2))

     (add-hook 'org-agenda-mode-hook 'k20e/org-agenda-mode-hook)
   #+END_SRC

*** Files

   #+BEGIN_SRC emacs-lisp
     (setq org-agenda-files (list (expand-file-name "work" org-directory)))
   #+END_SRC

*** Deadlines

   Non-nil means skip scheduling line if same entry shows because of deadline.

   In the agenda of today, an entry can show up multiple times because it is
   both scheduled and has a nearby deadline, and maybe a plain time stamp as
   well.

   When set to t, then only the deadline is shown and the fact that the entry
   is scheduled today or was scheduled previously is not shown.

   #+BEGIN_SRC emacs-lisp
     (setq org-agenda-skip-scheduled-if-deadline-is-shown nil)
   #+END_SRC

*** List

   Default to showing only today in the agenda list.

   #+BEGIN_SRC emacs-lisp
     (setq org-agenda-span 'day)
   #+END_SRC

** Habit

   #+BEGIN_SRC emacs-lisp
     (require 'org-habit)

     (setq org-habit-completed-glyph ?‚úì
           org-habit-today-glyph ?|)
   #+END_SRC

** Logging & Drawers

   Insert state change notes and time stamps into a drawer rather than simply
   "loose" after a headline.

   #+BEGIN_SRC emacs-lisp
     (setq org-log-into-drawer t)
   #+END_SRC
** Clock

   #+BEGIN_SRC emacs-lisp
     (defvar org-clock-idle-time 5)
   #+END_SRC

* Paradox

  #+BEGIN_SRC emacs-lisp
    (require 'face-remap)
    (require 'paradox)

    (defun k20e/paradox-menu-mode-hook ()
      (setq-default paradox-execute-asynchronously nil))

    (add-hook 'paradox-menu-mode-hook 'k20e/paradox-menu-mode-hook)
  #+END_SRC

* paredit-mode

  #+BEGIN_SRC emacs-lisp
    (autoload 'paredit-mode "paredit" nil t)

    ;; When to turn on paredit.
    (dolist (hook '(emacs-lisp-mode-hook
                    geiser-mode-hook
                    geiser-repl-mode-hook
                    lisp-mode-hook
                    scheme-mode-hook))
      (add-hook hook #'(lambda nil (paredit-mode 1))))

    (eval-after-load "paredit"
      '(progn
         (define-key paredit-mode-map [?\)] 'paredit-close-parenthesis)
         (define-key paredit-mode-map [(meta ?\))]
           'paredit-close-parenthesis-and-newline)
         (define-key paredit-mode-map (kbd "C-h") 'paredit-backward-delete)
         (define-key paredit-mode-map (kbd "C-j") 'join-line)))
  #+END_SRC

* TODO prettify-symbols-mode

  - https://blog.jft.rocks/emacs/unicode-for-orgmode-checkboxes.html
  - http://www.modernemacs.com/post/prettify-mode/
  - https://www.emacswiki.org/emacs/DontReadItsName
  - http://endlessparentheses.com/using-prettify-symbols-in-clojure-and-elisp-without-breaking-indentation.html
  - https://pixelambacht.nl/2015/sans-bullshit-sans/
  - https://github.com/ekaschalk/.spacemacs.d/blob/master/layers/display/local/pretty-fonts/pretty-fonts.el
  - https://unix.stackexchange.com/questions/247108/how-to-find-out-which-unicode-codepoints-are-defined-in-a-ttf-file

* python

  #+BEGIN_SRC emacs-lisp
    (require 'electric)
    (require 'blacken)
    (require 'flycheck)
    (require 'multiple-cursors)
    (require 'python)
    (require 'yasnippet)

    (defun k20e/python-mode-hook ()
      (setq display-line-numbers nil
            electric-indent-inhibit t       ; Do not drive me crazy with extra-dumb indentation!
            fill-column 88)
      (add-pragmatapro-prettify-symbols-alist)
      (prettify-symbols-mode 1)
      (setup-compose-predicate)

      (blacken-mode)
      (flycheck-mode 1)
      (superword-mode)
      (yas-minor-mode 1)
      ;; Previously:
      ;; C-M-f, C-M-b (paredit-forward/back)
      ;; C-M-n, C-M-p (forward-list/backward-list)
      ;; C-M-a, C-M-e (beginning-of-defun/end-of-defun)
      (define-key python-mode-map (kbd "M-a") 'python-nav-beginning-of-statement)
      (define-key python-mode-map (kbd "M-e") 'python-nav-end-of-statement)
      (define-key python-mode-map (kbd "M-n") 'python-nav-forward-statement)
      (define-key python-mode-map (kbd "M-p") 'python-nav-backward-statement)
      (define-key python-mode-map (kbd "M-q") 'blacken-buffer)
      (define-key python-mode-map (kbd "C-M-f") 'python-nav-forward-sexp)
      (define-key python-mode-map (kbd "C-M-b") 'python-nav-backward-sexp)
      (define-key python-mode-map (kbd "C-M-n") 'python-nav-forward-block)
      (define-key python-mode-map (kbd "C-M-p") 'python-nav-backward-block))

    (add-hook 'python-mode-hook 'k20e/python-mode-hook)
  #+END_SRC

** IPython

   #+BEGIN_SRC emacs-lisp
     (setq python-shell-interpreter "python"
           python-shell-interpreter-args ""
           python-shell-completion-native-enable nil)
   #+END_SRC

* re-builder

  #+BEGIN_SRC emacs-lisp
    (use-package re-builder
      :config
      (setq reb-re-syntax 'string))
  #+END_SRC

* recentf

  #+BEGIN_SRC emacs-lisp
    (require 'counsel)
    (require 'recentf)

    (setq recentf-save-file (expand-file-name ".recentf" k20e/google-drive-directory)
          recentf-max-saved-items 250)

    (recentf-mode 1)

    (global-set-key (kbd "C-x C-r") 'counsel-recentf)
  #+END_SRC

* reStructuredText

  #+BEGIN_SRC emacs-lisp
    (require 'rst)

    (defun k20e/rst-mode-hook ()
      (setq fill-column 78)
      (set-default 'rst-preferred-adornments '((?= simple 0)
                                               (?- simple 0)
                                               (?~ simple 0)
                                               (?* simple 0)
                                               (?+ simple 0)
                                               (?# simple 0)
                                               (?@ simple 0))))

    (add-hook 'rst-mode-hook 'k20e/rst-mode-hook)
  #+END_SRC

* ripgrep

  [[https://github.com/BurntSushi/ripgrep][ripgrep]] via [[#counsel][Counsel]], defaulting to the thing at point in the nearest
  =git= repository ancestor.

  #+BEGIN_SRC emacs-lisp
    (require 'counsel)

    (defun k20e/counsel-rg-project (initial-input)
      "Search using `counsel-rg' from the project root for INITIAL-INPUT."
      (interactive (list (thing-at-point 'symbol)))
      (counsel-rg initial-input (condition-case err
                                    (vc-root-dir)
                                  (error default-directory))))

    (global-set-key (kbd "C-x C-a") 'k20e/counsel-rg-project)
  #+END_SRC

* rust

  #+BEGIN_SRC emacs-lisp
    (require 'flycheck)
    (require 'flycheck-rust)
    (require 'rust-mode)

    (defun k20e/rust-mode-hook ()
      (setq flycheck-checker 'rust
            rust-format-on-save t)
      (flycheck-mode 1)
      (flycheck-rust-setup))

    (add-hook 'rust-mode-hook 'k20e/rust-mode-hook)
  #+END_SRC

* savehist

  #+BEGIN_SRC emacs-lisp
    ;; Save minibuffer history.
    (require 'savehist)

    (setq savehist-file (expand-file-name ".savehist" k20e/google-drive-directory))
    (savehist-mode)
  #+END_SRC

* =*scratch*=

  Begin with an empty =*scratch*= file.

  #+BEGIN_SRC emacs-lisp
    (setq initial-scratch-message nil)
  #+END_SRC

  Set it to Emacs Lisp mode.

  #+BEGIN_SRC emacs-lisp
    (with-current-buffer (get-buffer-create "*scratch*")
      (emacs-lisp-mode))
  #+END_SRC

** Quickly create new scratch buffers

   With a preset list of major modes that I find often need scratch
   pads for.

   #+BEGIN_SRC emacs-lisp
     (defconst k20e/scratch-buffer-modes
       '(fundamental-mode
         emacs-lisp-mode
         python-mode
         javascript-mode
         org-mode
         sql-mode
         text-mode
         yaml-mode)
       "Common major modes to create scratch buffers for.")

     (defun k20e/scratch-buffer ()
       "Generate a new scratch buffer.
     Choose from `k20e/scratch-buffer-modes' list of major modes to
     enable in the newly created scratch buffer and switch to it."
       (interactive)
       (let ((mode (read (ivy-completing-read "New *scratch* buffer with mode: "
                                              (mapcar (lambda (el) (format "%s" el))
                                                      k20e/scratch-buffer-modes)))))
         (switch-to-buffer (generate-new-buffer (format "*scratch-%s*" mode)))
         (funcall mode)))
   #+END_SRC

   Bind it globally.

   #+BEGIN_SRC emacs-lisp
     (global-set-key (kbd "<f10>") 'k20e/scratch-buffer)
   #+END_SRC

* sh-mode

  #+BEGIN_SRC emacs-lisp
    (require 'flycheck)
    (require 'sh-script)

    (defun k20e/sh-script-mode-hook ()
      (add-pragmatapro-prettify-symbols-alist)
      (prettify-symbols-mode 1)
      (flycheck-mode 1)

      (setup-compose-predicate))

    (add-hook 'sh-mode-hook 'k20e/sh-script-mode-hook)
  #+END_SRC

* server

  #+BEGIN_SRC emacs-lisp
    ;; Start the Emacs server.
    (require 'server)

    (unless (server-running-p)
      (server-start))
  #+END_SRC

* smex

  #+BEGIN_SRC emacs-lisp
    (require 'smex)
    (smex-initialize)

    ;; Share smex history across my machines.
    (setq smex-save-file (expand-file-name ".smex-items" k20e/google-drive-directory))
  #+END_SRC

* sql-mode

  #+BEGIN_SRC emacs-lisp
    (require 'sql)

    (defun k20e/sql-mode-hook ()
      (setq sql-product 'mysql
            tab-width 4)
      (sql-highlight-mysql-keywords))

    (add-hook 'sql-mode-hook 'k20e/sql-mode-hook)
  #+END_SRC

* server

  #+BEGIN_SRC emacs-lisp
    ;; Start the Emacs server.
    (require 'server)

    (unless (server-running-p)
      (server-start))
  #+END_SRC

* Swiper

  #+BEGIN_SRC emacs-lisp
    (require 'swiper)

    (global-set-key (kbd "C-s") 'swiper)
  #+END_SRC

* Theme

  Always highlight the current line:

  #+BEGIN_SRC emacs-lisp
    (require 'hl-line)

    (global-hl-line-mode t)
  #+END_SRC

  Simple stuff:

  #+BEGIN_SRC emacs-lisp
    (require 'simple)

    (line-number-mode)
    (column-number-mode)

    (size-indication-mode)
  #+END_SRC

  Highlight matching parentheses:

  #+BEGIN_SRC emacs-lisp
    (require 'paren)

    (show-paren-mode)
  #+END_SRC

  Do not blink the cursor:

  #+BEGIN_SRC emacs-lisp
    (require 'frame)

    (setq-default blink-cursor-mode 0)
  #+END_SRC

  Truncate lines and enable fringes to indicate truncated lines:

  #+BEGIN_SRC emacs-lisp
    (require 'fringe)

    (fringe-mode)

    (setq-default
     truncate-lines t
     truncate-partial-width-windows nil)
  #+END_SRC

  No bell:

  #+BEGIN_SRC emacs-lisp
    (setq-default ring-bell-function 'ignore)
  #+END_SRC

  Make sure syntax highlighting is enabled:

  #+BEGIN_SRC emacs-lisp
    (require 'font-core)

    (global-font-lock-mode)
  #+END_SRC

  Set theme:

  #+BEGIN_SRC emacs-lisp
    (require 'nord-theme)

    (load-theme 'nord t)
  #+END_SRC

* term
  :PROPERTIES:
  :CUSTOM_ID: term
  :END:

  Together with [[#multi-term][multi-term]] this sets up my terminal environment within Emacs.

** Key Bindings

   =term-raw-map= is used in /char/ mode.

   #+BEGIN_SRC emacs-lisp
     (require 'counsel)
     (require 'smex)

     (define-key term-raw-map (kbd "M-x") 'counsel-M-x)
     (define-key term-raw-map (kbd "M-h") 'backward-kill-word)
   #+END_SRC

   =term-mode-map= is used in /line/ mode.

   #+BEGIN_SRC emacs-lisp
     (define-key term-mode-map (kbd "M-x") 'counsel-M-x)
   #+END_SRC

* terraform-mode

  #+BEGIN_SRC emacs-lisp
    (require 'terraform-mode)

    (defun k20e/terraform-mode-hook ()
      (setq terraform-format-on-save-mode t))

    (add-hook 'terraform-mode-hook 'k20e/terraform-mode-hook)
  #+END_SRC

* text-mode

  #+BEGIN_SRC emacs-lisp
    (defun k20e/text-mode-hook ()
      (auto-fill-mode 1))

    (add-hook 'text-mode-hook 'k20e/text-mode-hook)
  #+END_SRC

* toml-mode

  #+BEGIN_SRC emacs-lisp
    (add-to-list 'auto-mode-alist '("Pipfile\\'" . toml-mode))
  #+END_SRC

* tramp

** =ControlPath=

   Fix =ControlPath too long= errors due to OS X pitching a [[https://lists.macosforge.org/pipermail/macports-tickets/2011-June/084295.html][long temporary directory]] to =ssh=.

   Unfortunately, setting this is blowing up the =server-start= which
   can no longer find the socket stored in the original =TMPDIR=
   value.

   #+BEGIN_SRC emacs-lisp
     ;; (setenv "TMPDIR" "/tmp")
   #+END_SRC

   Eureka!  It appears that the =ControlMaster= option for =ssh=
   should be set to =yes= instead of =auto= to avoid the =ControlPath
   too long= error.  Here is the interesting section of =man 5
   ssh_config=:

   #+BEGIN_EXAMPLE
     ControlMaster
                  Enables the sharing of multiple sessions over a single network connection.  When set to
                  ``yes'', ssh(1) will listen for connections on a control socket specified using the
                  ControlPath argument.  Additional sessions can connect to this socket using the same
                  ControlPath with ControlMaster set to ``no'' (the default).  These sessions will try to reuse
                  the master instance's network connection rather than initiating new ones, but will fall back
                  to connecting normally if the control socket does not exist, or is not listening.

                  Setting this to ``ask'' will cause ssh to listen for control connections, but require confir-
                  mation using the SSH_ASKPASS program before they are accepted (see ssh-add(1) for details).
                  If the ControlPath cannot be opened, ssh will continue without connecting to a master
                  instance.

                  X11 and ssh-agent(1) forwarding is supported over these multiplexed connections, however the
                  display and agent forwarded will be the one belonging to the master connection i.e. it is not
                  possible to forward multiple displays or agents.

                  Two additional options allow for opportunistic multiplexing: try to use a master connection
                  but fall back to creating a new one if one does not already exist.  These options are:
                  ``auto'' and ``autoask''.  The latter requires confirmation like the ``ask'' option.
   #+END_EXAMPLE

   The =tramp-ssh-controlmaster-options= variable is responsible for
   the =ControlMaster= value as well as a few other options which have
   not been changed from their default values.

   #+BEGIN_SRC emacs-lisp
     (setq tramp-ssh-controlmaster-options
           "-o ControlPath=%t.%%r@%%h:%%p -o ControlMaster=yes -o ControlPersist=no")
   #+END_SRC

** Inline Copying

   Do not inline copy files.  This is to avoid =File exists, but
   cannot be read= errors.

   #+BEGIN_SRC emacs-lisp
     (setq-default tramp-copy-size-limit -1)
   #+END_SRC

** File Backup

   Do not backup files edited by =tramp= to [[http://www.gnu.org/software/emacs/manual/html_node/tramp/Auto_002dsave-and-Backup.html][avoid possibly sharing
   copies of privileged files with non-privileged users]].

   #+BEGIN_SRC emacs-lisp
     (add-to-list 'backup-directory-alist (list tramp-file-name-regexp))
   #+END_SRC

** Debugging

   Level =3= by default.

   #+BEGIN_SRC emacs-lisp
     ;; (setq tramp-verbose 6)
   #+END_SRC

   Will create a detailed log buffer.

* TypeScript

  #+BEGIN_SRC emacs-lisp
    (require 'eldoc)
    (require 'flycheck)
    (require 'tide)
    (require 'typescript-mode)

    (defun k20e/typescript-mode-hook ()
      (eldoc-mode)
      (flycheck-mode 1)
      (tide-setup)
      (tide-hl-identifier-mode 1))

    (add-hook 'typescript-mode-hook 'k20e/typescript-mode-hook)

    (add-hook 'before-save-hook 'tide-format-before-save)
  #+END_SRC

* uniquify

  Name multiple identical buffer names in a sensible manner.

  #+BEGIN_SRC emacs-lisp
    (require 'uniquify)

    (setq uniquify-buffer-name-style 'forward)
  #+END_SRC

* windmove

  #+BEGIN_SRC emacs-lisp
    (require 'windmove)

    (windmove-default-keybindings 'hyper)
    (setq windmove-wrap-around t)

    (global-set-key (kbd "M-SPC") 'windmove-right)
    (global-set-key (kbd "M-S-SPC") 'windmove-left)
  #+END_SRC

* web-mode

  #+BEGIN_SRC emacs-lisp
    (require 'elec-pair)
    (require 'web-mode)

    (add-to-list 'auto-mode-alist '("\\.html?\\'" . web-mode))

    (defun k20e/web-mode-hook ()
      (set-default 'web-mode-engines-alist '(("django" . "\\.html?\\'")))
      (setq web-mode-markup-indent-offset 2
            web-mode-enable-auto-quoting nil)

      ;; Disable pairing { to avoid {{ }}}} from web-mode block templates
      (setq-local electric-pair-inhibit-predicate
                  (lambda (c)
                    (if (char-equal c ?{) t (electric-pair-default-inhibit c)))))

    (add-hook 'web-mode-hook 'k20e/web-mode-hook)
  #+END_SRC

* winner-mode

  Remember window configurations.

  #+BEGIN_SRC emacs-lisp
    (require 'winner)

    (winner-mode)
  #+END_SRC

* whitespace

  Take care of some whitespace issues.

  - Kill trailing whitespace on save
  - Insert a new line at the end of file on save
  - Prefer =space= over =tab=

  #+BEGIN_SRC emacs-lisp
    (require 'whitespace)

    (defun k20e/before-save-hook ()
      (whitespace-cleanup)
      (delete-trailing-whitespace 0 nil))

    (add-hook 'before-save-hook 'k20e/before-save-hook)

    (set-default 'indent-tabs-mode nil)

    (setq require-final-newline t
          mode-require-final-newline t)
  #+END_SRC

* yaml-mode

  #+BEGIN_SRC emacs-lisp
    (require 'highlight-indent-guides)
    (require 'yaml-mode)
    (require 'flycheck)
    (require 'flycheck-yamllint)
    (require 'flyspell)

    (defun k20e/yaml-mode-hook ()
      (setq fill-column 118)
      (define-key yaml-mode-map (kbd "C-m") 'newline-and-indent)
      (flycheck-mode 1)
      (flycheck-yamllint-setup)
      (flyspell-mode-off)
      (highlight-indent-guides-mode)
      (add-pragmatapro-prettify-symbols-alist)
      (setup-compose-predicate)
      (prettify-symbols-mode 1))

    (add-hook 'yaml-mode-hook 'k20e/yaml-mode-hook)
  #+END_SRC
