#+TITLE: Literate =.emacs.d=
#+OPTIONS: toc:nil num:nil

#+BEGIN_QUOTE
[[http://www.literateprogramming.com/][Literate programming]] is a methodology that combines a programming language
with a documentation language, thereby making programs more robust, more
portable, more easily maintained, and arguably more fun to write than programs
that are written only in a high-level language. The main idea is to treat a
program as a piece of literature, addressed to human beings rather than to a
computer. The program is also viewed as a hypertext document, rather like the
World Wide Web. (Indeed, I used the word WEB for this purpose long before CERN
grabbed it!) -- [[http://www-cs-faculty.stanford.edu/~uno/lp.html][Donald Knuth]]
#+END_QUOTE

All configuration described here is performed in an [[https://github.com/krismolendyke/.emacs.d/blob/0d5a5434ff79d48ab613fc433d0ae2443c552665/init.el#L88][=after-init-hook=]]
function.

#+TOC: headlines 2

* Build Emacs
  :PROPERTIES:
  :CUSTOM_ID: build-emacs
  :END:

  I primarily use Emacs locally on Apple hardware.  Most, if not all,
  of the remote editing that I do is done via =tramp=.  I keep an eye
  on the [[http://git.savannah.gnu.org/cgit/emacs.git/log/][log]] at the official Emacs [[http://git-scm.com/][git]] [[http://git.savannah.gnu.org/cgit/emacs.git/][repository]].  When something
  interesting lands there I will build a fresh Emacs installation via
  [[http://brew.sh/][Homebrew]] and try it out.

  #+BEGIN_SRC sh
    brew info emacs
  #+END_SRC

  I install Emacs with the following options.

  #+BEGIN_SRC sh
    time brew install emacs --with-cocoa --with-gnutls --HEAD
  #+END_SRC

  If there is an existing installation it needs to be removed first.

  #+BEGIN_SRC sh
    brew uninstall emacs
  #+END_SRC

** Emacs Source Directory

   =source-directory= is helpful to have set properly when exploring built-in
   /C functions/ via =find-function=.

   Finding the source code directory via Homebrew can be done with the
   following command:

   #+BEGIN_SRC sh :exports both
     echo $(brew --cache)/emacs*
   #+END_SRC

   #+RESULTS:
   : /Library/Caches/Homebrew/emacs--git

   /Note/: =source-directory= must be set during Emacs initialization time to
   persist.  See [[https://github.com/krismolendyke/.emacs.d/blob/1241a848cee7dadfa0c719643925fa0a7b86f476/init.el#L84-L86][=init.el=]].

* How this works
  :PROPERTIES:
  :CUSTOM_ID: how-this-works
  :END:

  The [[https://github.com/krismolendyke/.emacs.d/blob/master/init.el][=init.el=]] file bootstraps the entire process.  It is found by
  Emacs automatically if it is located in one of several well-defined
  locations.

  #+BEGIN_QUOTE
  When Emacs is started, it normally tries to load a Lisp program from
  an "initialization file", or "init file" for short.  This file, if
  it exists, specifies how to initialize Emacs for you.  Emacs looks
  for your init file using the filenames `~/.emacs', `~/.emacs.el', or
  `~/.emacs.d/init.el'; you can choose to use any one of these three
  names (*note Find Init::).  Here, `~/' stands for your home
  directory. -- [[http://www.gnu.org/software/emacs/manual/html_node/emacs/Init-File.html][The Emacs Initialization File]]
  #+END_QUOTE

  Once it's found, =init.el= sets "global" variables and defines
  several functions to be executed /during/ initialization and one
  that should be executed /after/ initialization has completed.
  =k20e/after-init-hook= is a hook run after initialization.  The most
  interesting bit of this function is the call to
  =org-babel-load-file=:

  #+BEGIN_QUOTE
  (org-babel-load-file FILE &optional COMPILE)

  Load Emacs Lisp source code blocks in the Org-mode FILE. This
  function exports the source code using `org-babel-tangle' and then
  loads the resulting file using `load-file'.  With prefix arg
  (noninteractively: 2nd arg) COMPILE the tangled Emacs Lisp file to
  byte-code before it is loaded.
  #+END_QUOTE

  This function invocation exports all [[http://orgmode.org/manual/Working-With-Source-Code.html#Working-With-Source-Code][=org-mode= source blocks]] in any
  =org-mode= files in the =.emacs.d= directory to a single Emacs Lisp
  file and then instructs Emacs to load that file.  [[https://github.com/krismolendyke/.emacs.d/blob/master/custom.org][=custom.org=]] is
  the biggest of those =org-mode= files and ‚Ä¶ /this/ is that file!

** HTML Export =post-commit= Hook

   This [[https://git-scm.com/book/en/v2/Customizing-Git-Git-Hooks][git =post-commit= hook]] is located at =.git/hooks/post-commit=
   and automatically generates the HTML content of this [[https://pages.github.com/][GitHub Pages]]
   site.

   #+BEGIN_SRC sh
     #!/bin/sh

     emacs --batch \
           --directory '~/.emacs.d/site-lisp/org-mode/lisp' \
           --load '~/.emacs.d/elisp/k20e-org-html-export.el' \
           --visit '~/.emacs.d/custom.org' \
           --execute '(org-html-export-to-html)' 2>&1
     rm -f *~
     git checkout gh-pages
     mv custom.html index.html
     git add index.html
     git commit --message "post-commit hook regeneration."
     git checkout master
   #+END_SRC

   All of the interesting customization of the exported document is
   performed in [[https://github.com/krismolendyke/.emacs.d/blob/master/elisp/k20e-org-html-export.el][=elisp/k20e-org-html-export.el=]].

* Customization

  I don't make much use of the [[http://www.gnu.org/software/emacs/manual/html_node/emacs/Customization.html#Customization][Customization]] interface.  By default it dumps
  its settings into =init.el= and I don't want to see them there.  A few
  packages that I use do rely on customization so I do need it to be loaded.

  #+BEGIN_SRC emacs-lisp
    (setq custom-file (expand-file-name ".emacs-custom.el" user-emacs-directory))
    (load custom-file)
  #+END_SRC

* Cask

  #+BEGIN_QUOTE
  Cask is a project management tool for Emacs Lisp to automate the
  package development cycle; development, dependencies, testing,
  building, packaging and more.

  Cask can also be used to manage dependencies for your local Emacs
  configuration. -- [[http://cask.readthedocs.org/en/latest/][Cask documentation]]
  #+END_QUOTE

** Installation

   #+BEGIN_SRC sh
     brew install cask
   #+END_SRC

   After building a new Emacs version =cask= should be used to update
   dependencies.

   #+BEGIN_SRC sh
     cd ~/.emacs.d
     cask install
   #+END_SRC

   New dependencies will be installed into =~/.emacs.d/.cask=.

* Pallet

  #+BEGIN_QUOTE
  Pallet is a package management helper for Emacs. -- [[https://github.com/rdallasgray/pallet][Pallet README]]
  #+END_QUOTE

  As packages are installed, updated or removed via =list-packages=,
  Pallet maintains changes to the =Cask= file automatically.

* Global GNU Emacs Key Bindings

  These global key bindings override /built-in/ functions only.
  Package-specific or custom function defunition key bindings are made in
  their own dedicated sections where other specific settings are made.

** Unset

   OS X annoyance -- =C-M-d= is a "hot key" bound to dictionary lookup
   and masks the key binding in Emacs.  [[http://apple.stackexchange.com/questions/22785/how-do-i-disable-the-command-control-d-word-definition-keyboard-shortcut-in-os-x/114269#114269][Disabling it]] can currently
   only be done by editing a default /and restarting/.

   #+BEGIN_SRC sh
     defaults write com.apple.symbolichotkeys AppleSymbolicHotKeys \
              -dict-add 70 '<dict><key>enabled</key><false/></dict>'
   #+END_SRC

   #+BEGIN_SRC emacs-lisp
     (global-unset-key (kbd "<f1> h"))
     (global-unset-key (kbd "<f11>"))
     (global-unset-key (kbd "C-h"))
     (global-unset-key (kbd "C-q"))
     (global-unset-key (kbd "C-z"))
     (global-unset-key (kbd "M-`"))
     (global-unset-key (kbd "M-c"))
     (global-unset-key (kbd "M-h"))
     (global-unset-key (kbd "M-u"))
   #+END_SRC

** Set

   #+BEGIN_SRC emacs-lisp
     (global-set-key (kbd "<f1> F") 'find-function)
     (global-set-key (kbd "<f1> V") 'find-variable)
     (global-set-key (kbd "<f7>") 'previous-error) ;; ‚óÄ‚óÄ
     (global-set-key (kbd "<f9>") 'next-error) ;; ‚ñ∂‚ñ∂
     (global-set-key (kbd "C-S-h") 'kill-whole-line)
     (global-set-key (kbd "C-c DEL") 'join-line)
     (global-set-key (kbd "C-h") 'delete-backward-char)
     (global-set-key (kbd "C-j") 'join-line)
     (global-set-key (kbd "C-x C-t") 'transpose-lines)
     (global-set-key (kbd "H-h H-f") 'find-function)
     (global-set-key (kbd "H-h H-v") 'find-variable)
     (global-set-key (kbd "H-t") 'toggle-frame-fullscreen)
     (global-set-key (kbd "M-+") 'text-scale-adjust)
     (global-set-key (kbd "M-.") 'imenu)
     (global-set-key (kbd "M-/") 'hippie-expand)
     (global-set-key (kbd "M-`") 'other-window)
     (global-set-key (kbd "M-h") 'backward-kill-word)
     (global-set-key (kbd "M-t") 'transpose-words)
   #+END_SRC

* k20e Defaults

** Apropos

   Sort by relevancy.

   #+BEGIN_SRC emacs-lisp
     (setq-default apropos-sort-by-scores t)
   #+END_SRC

** =cycle-spacing=

   #+BEGIN_SRC emacs-lisp
     (defun k20e/cycle-spacing (&optional n)
       "Make `cycle-spacing' operate in `fast' mode."
       (interactive "*p")
       (cycle-spacing n nil 'fast))

     (global-set-key (kbd "M-SPC") 'k20e/cycle-spacing)
   #+END_SRC

** Backup Files

   Back up files to a single location.

   #+BEGIN_SRC emacs-lisp
     (defvar k20e/backup-dir (expand-file-name "backup" user-emacs-directory)
       "A single directory for storing backup files within.")

     (unless (file-exists-p k20e/backup-dir) (make-directory k20e/backup-dir))

     (setq backup-by-copying t
           backup-directory-alist `(("." . ,k20e/backup-dir))
           delete-old-versions t
           version-control t)
   #+END_SRC

** Enabled Commands

   Commands disabled by default prompt at first use.  Enabling
   commands disables the prompt.

   #+BEGIN_SRC emacs-lisp
     (defvar k20e/enabled-commands
       '(downcase-region
         upcase-region
         narrow-to-region
         narrow-to-page
         scroll-left
         scroll-right)
       "Normally disabled commands.")

     (defun k20e/enable-commands ()
       "Enabled normally disabled commands."
       (dolist (command k20e/enabled-commands)
         (put command 'disabled nil)))

     (k20e/enable-commands)
   #+END_SRC

** Inferior Shell

   I use [[https://github.com/krismolendyke/.zsh][Zsh]] in [[#multi-term][=multi-term=]] but I've customized it so much that it
   often interferes with simple inferior shell tasks.  Defaulting to
   =sh= seems to work well.

   #+BEGIN_SRC emacs-lisp
     (setq shell-file-name "/bin/sh")
   #+END_SRC

** TODO Defaults for Review

   This is a bunch of stuff that I just dumped here and need to go through yet.

   Show the active region and delete it when selected if a character
   is inserted.

   #+BEGIN_SRC emacs-lisp
     (transient-mark-mode t)
     (delete-selection-mode 1)
   #+END_SRC

   "Electric" indentation is generally what I consider to be sensible.

   #+BEGIN_SRC emacs-lisp
     (electric-indent-mode)
   #+END_SRC

   Cycle through the mark ring faster.

   #+BEGIN_SRC emacs-lisp
     (setq set-mark-command-repeat-pop t)
   #+END_SRC

   Splitting windows horizontally makes more sense on all of the wide
   screen monitors I work on.

   #+BEGIN_SRC emacs-lisp
     (setq split-width-threshold 81)
   #+END_SRC

   #+BEGIN_SRC emacs-lisp
     ;; What's going on here?
     (setq echo-keystrokes 0.1)


     ;; Where am I?
     (line-number-mode t)
     (global-hl-line-mode t)
     (column-number-mode t)
     (show-paren-mode t)

     ;; Do not break lines.  Truncate them.
     (setq-default truncate-lines t)

     ;; Automatically reload buffers when files change on disk.
     (global-auto-revert-mode 1)

     ;; Quit all that damn racket!
     (setq ring-bell-function 'ignore)

     ;; Make sure syntax highlighting is enabled.
     (global-font-lock-mode t)

     ;; y is the new yes.  n is the new no.
     (defalias 'yes-or-no-p 'y-or-n-p)
   #+END_SRC

* k20e Custom Functions

  I have found these to be useful enough to keep around permanently.

** Editing

   #+BEGIN_SRC emacs-lisp
     (defun k20e/mark-current-line ()
       "Mark the current line.
     If the mark is already set simply move the point forward a single
     line.  If it is not set, set it at the beginning of the current
     line and then move the point forward a single line."
       (interactive)
       (unless mark-active
         (beginning-of-line)
         (set-mark (point)))
       (forward-line 1))

     (defun k20e/open-line-below ()
       "Insert a new line below the current line."
       (interactive)
       (end-of-line)
       (newline)
       (indent-for-tab-command))

     (defun k20e/open-line-above ()
       "Insert a new line above the current line."
       (interactive)
       (beginning-of-line)
       (newline)
       (forward-line -1)
       (indent-for-tab-command))

     ;; Inspired by http://whattheemacsd.com/key-bindings.el-01.html
     (defun k20e/goto-linum ()
       "Show line numbers and prompt for a line number to go to."
       (interactive)
       (let ((linum-mode-previous-state
              (if (and (boundp 'linum-mode) linum-mode) 1 -1)))
         (unwind-protect
             (progn
               (linum-mode 1)
               (call-interactively 'goto-line)
               (linum-mode linum-mode-previous-state))
           (linum-mode linum-mode-previous-state))))
   #+END_SRC

   This one is stolen from [[https://github.com/magnars/.emacs.d/blob/e56e71ce0f0791c7237192a049f29c2de686409a/defuns/lisp-defuns.el][magnars]]:

   #+BEGIN_SRC emacs-lisp
     (defun k20e/eval-and-replace ()
       "Replace the preceding sexp with its value."
       (interactive)
       (backward-kill-sexp)
       (condition-case nil
           (prin1 (eval (read (current-kill 0)))
                  (current-buffer))
         (error (message "Invalid expression")
                (insert (current-kill 0)))))
   #+END_SRC

   Bind editing functions:

   #+BEGIN_SRC emacs-lisp
     (global-set-key (kbd "M-l") 'k20e/mark-current-line)
     (global-set-key (kbd "<M-return>") 'k20e/open-line-below)
     (global-set-key (kbd "<M-S-return>") 'k20e/open-line-above)
     (global-set-key [remap goto-line] 'k20e/goto-linum)
   #+END_SRC

** Buffers

   #+BEGIN_SRC emacs-lisp
     (defun k20e/display-buffer-file-name ()
       "Message the full path to the currently visited file."
       (interactive)
       (message "%s" (buffer-file-name)))
   #+END_SRC

*** Toggle Source/Test Buffer

    If this gets any smarter it should be refactored into its own
    package.

    #+BEGIN_SRC emacs-lisp
      (defun k20e/test-buffer-p ()
        "Is the current buffer a test buffer?
      This function na√Øvely assumes that the file name suffix '_test'
      is indicative of a test file."
        (string-suffix-p
         "_test"
         (file-name-sans-extension (buffer-file-name))))

      (defun k20e/switch-to-test-buffer ()
        "Switch to the test buffer associated with the current source buffer."
        (let ((d (file-name-directory (buffer-file-name)))
              (f (format "%s_test.%s"
                          (file-name-sans-extension (buffer-name))
                          (file-name-extension (buffer-file-name)))))
          (find-file (expand-file-name f d))))

      (defun k20e/switch-to-source-buffer ()
        "Switch to the source buffer associated with the current test buffer."
        (let ((e (file-name-extension (buffer-file-name)))
              (f (car (split-string (file-name-sans-extension (buffer-file-name))
                                    "_"))))
          (find-file (format "%s.%s" f e))))

      (defun k20e/toggle-test-buffer ()
        "Toggle between a source and test buffer.
      This function na√Øvely assumes that the file name suffix '_test'
      is indicative of a test file.  Therefore it should only be useful
      in major modes where that convention is expected."
        (interactive)
        (if (k20e/test-buffer-p)
            (k20e/switch-to-source-buffer)
          (k20e/switch-to-test-buffer)))
    #+END_SRC

** Windows

   #+BEGIN_SRC emacs-lisp
     (require 'ido)

     (defun split-window-right-and-balance-and-go-there-and-switch-buffer (&optional arg)
       "Optional argument ARG Prefix argument will switch buffer using ido."
       (interactive "P")
       (split-window-right)
       (balance-windows-area)
       (windmove-right)
       (if arg
           (ido-switch-buffer)
         (switch-to-buffer nil)))

     (defun delete-window-and-balance ()
       "Balance windows after deleting."
       (interactive)
       (delete-window)
       (balance-windows-area))
   #+END_SRC

   Bind window functions:

   #+BEGIN_SRC emacs-lisp
     (global-set-key (kbd "C-x 0") 'delete-window-and-balance)
     (global-set-key (kbd "C-x 3") 'split-window-right-and-balance-and-go-there-and-switch-buffer)
   #+END_SRC

** Networking

   #+BEGIN_SRC emacs-lisp
     (require 'net-utils)
     (require 'tramp)

     (defun k20e/known-hosts ()
       "Get a host name from ~./ssh/known_hosts file."
       (completing-read "host: "
                        (let ((value))
                          (dolist (elt (tramp-parse-shosts "~/.ssh/known_hosts") value)
                            (if elt (setq value (cons (cadr elt) value)))))))

     (defun k20e/host-ip ()
       "Insert the current IP of a host using `dns-lookup-program'.
     Similar to but simpler than `dns-lookup-host'."
       (interactive)
       (let ((host (k20e/known-hosts)))
         (insert (car (last (split-string (shell-command-to-string
                                           (concat dns-lookup-program " " host))))))))
   #+END_SRC

** Lunar üåô

   #+BEGIN_SRC emacs-lisp
     (require 'calendar)
     (require 'lunar)

     (defun k20e/full-moons-info ()
       "Get a list of upcoming full moons info beginning with the current month.
     See `lunar-phase-list' and `lunar-phase-name'."
       (let* ((current-date (calendar-current-date))
              (current-month (car current-date))
              (current-year (car (last current-date)))
              (full-moon-phase-index 2)
              (k20e/full-moons-info '()))
         (dolist (phase (lunar-phase-list current-month current-year))
           (if (= (car (last phase)) full-moon-phase-index)
               (setq k20e/full-moons-info (cons phase k20e/full-moons-info))))
         (reverse k20e/full-moons-info)))

     (defun k20e/full-moons ()
       "Display upcoming full moons beginning with the current month."
       (interactive)
       (with-output-to-temp-buffer "*full-moons*"
         (princ
          (mapconcat
           #'(lambda (x)
               (format "%s %s" (calendar-date-string (car x)) (car (cdr x))))
           (k20e/full-moons-info)
           "\n"))))
   #+END_SRC

* OS X

  These may be better suited split up to key bindings and/or a maybe
  input/mouse section?

  #+BEGIN_SRC emacs-lisp
    ;; I spend most of my time in OS X.
    (if (equal system-type 'darwin)
        (progn
          ;; Command as meta.
          (setq ns-command-modifier 'meta)

          ;; Option as hyper.
          (setq ns-option-modifier 'hyper)

          ;; fn as super.
          (setq ns-function-modifier 'super)

          ;; See https://github.com/Homebrew/homebrew/commit/49c85b89753d42cc4ec2fee9607a608b3b14ab33?w=1
          (setq ns-use-srgb-colorspace t)

          ;; Trackpad taming.
          (setq
           mouse-wheel-scroll-amount '(0.0001)
           mouse-wheel-progressive-speed nil
           scroll-step 1
           scroll-conservatively 10000
           auto-window-vscroll nil)

          ;; Trash.
          (setq trash-directory (expand-file-name "~/.Trash")
                delete-by-moving-to-trash t)))
  #+END_SRC

* Appearance

** Theme

   #+BEGIN_SRC emacs-lisp
     ;; Add themes.
     (dolist
         (theme (directory-files (expand-file-name "themes" user-emacs-directory) t "\\w+"))
       (when (file-directory-p theme)
         (add-to-list 'custom-theme-load-path theme)))

     ;; Tomorrow as a submodule.  It has a bunch of other editor support.
     (add-to-list 'custom-theme-load-path
                  (expand-file-name
                   "themes/tomorrow/GNU Emacs" user-emacs-directory))
     (add-to-list 'load-path (expand-file-name
                              "themes/tomorrow/GNU Emacs" user-emacs-directory))

     ;; These ports of Sublime Text 2 themes required a stupid shell script
     ;; to "install" them which I refuse to use.
     (add-to-list 'custom-theme-load-path
                  (expand-file-name
                   "themes/st2/themes" user-emacs-directory))

     (defvar k20e/theme-light 'solarized-light
       "The default lightly colored theme.

     Other good light candidates:

       - tomorrow-day
       - whiteboard")

     (defvar k20e/theme-dark 'base16-tomorrow
       "The default darkly colored theme.

     Other good dark candidates:

       - tomorrow-night-bright
       - tomorrow-night
       - hickey
       - fogus
       - dorsey
       - wilson
       - wombat
       - zenburn")

     (defun k20e/theme-load-light ()
       "Load a lightly colored theme for conditions when ambient light
     is bright."
       (interactive)
       (disable-theme (car custom-enabled-themes))
       (load-theme k20e/theme-light t)
       (set-face-background 'hl-line "AntiqueWhite2"))

     (defun k20e/theme-load-dark ()
       "Load a darkly colored theme for conditions when ambient light
     is dark."
       (interactive)
       (require 'linum)
       (disable-theme (car custom-enabled-themes))
       (load-theme k20e/theme-dark t)
       (set-face-background 'hl-line "gray20")
       (set-face-background 'fringe "gray20")
       (set-face-background 'linum "gray20")
       (set-face-background 'region "gray36")
       (set-face-background 'show-paren-match "gray64"))

     (defun k20e/theme-toggle ()
       "Switch between the light and dark theme."
       (interactive)
       (if (member k20e/theme-dark custom-enabled-themes)
           (k20e/theme-load-light)
         (k20e/theme-load-dark)))

     ;; Load a dark theme by default.
     (k20e/theme-load-dark)
   #+END_SRC

   Bind toggle function:

   #+BEGIN_SRC emacs-lisp
     (global-set-key (kbd "C-x t") 'k20e/theme-toggle)
   #+END_SRC

*** TODO Try out [[https://github.com/bruce/emacs-spacegray-theme][Spacegray theme]]

** Cursor

   Disable blinking.

   #+BEGIN_SRC emacs-lisp
     (blink-cursor-mode 0)
   #+END_SRC

   If blinking is enabled the rate can be adjusted.

   #+BEGIN_SRC emacs-lisp
     (setq blink-cursor-interval 0.75)
   #+END_SRC

** Frame Height

   These functions were more useful before I began using the [[http://git.savannah.gnu.org/cgit/emacs.git/tree/lisp/desktop.el][=desktop=]] package
   and its [[http://git.savannah.gnu.org/cgit/emacs.git/tree/lisp/desktop.el?id=e78d7f87377e29ee7ed2dd7aaed40244f1edbf13#n397][=desktop-restore-frames=]] variable.

   #+BEGIN_SRC emacs-lisp
     (defun get-max-rows (pixel-height)
       "Return the maximum number of rows that will fit with this screen.
     Given a screen pixel height at the current frame character height, calculate
     the maximum number of rows that will fit with that height."
       (if (window-system)
           (/ pixel-height (frame-char-height))))

     (defun set-frame-height-to-max ()
       "Set the selected frame height to the maximum that will fit the current
     screen resolution."
       (if (window-system)
           (set-frame-height (selected-frame)
                             (get-max-rows (- (display-pixel-height) 44)))))

     (set-frame-height-to-max)
   #+END_SRC

** Fringe

   The "fringe" or "gutter" shows indicators for wrapped/extended lines,
   [[#flycheck][flycheck]], etc.

   #+BEGIN_SRC emacs-lisp
     (require 'fringe)
     (fringe-mode (cdr (assoc "half-width" fringe-styles)))
   #+END_SRC

** TODO Disable =linum-mode= when text scale is not zero

   The fringe text scale is adjusted with the rest of the buffer yet the
   fringe width is not adjusted.  If text scale is positive the line numbers
   get cut-off and not very useful.  There is [[http://stackoverflow.com/questions/9304192/emacs-linum-mode-and-size-of-font-unreadable-line-numbers][a hack that tries to address
   this situation]] but I'd just as soon disable line numbering when the text
   scale is large.

* ag

  [[https://github.com/ggreer/the_silver_searcher][The Silver Searcher]] is similar to =ack=, which in turn is similar to =grep=.

  #+BEGIN_SRC emacs-lisp
    (require 'ag)

    (setq ag-arguments
          '("--smart-case" "--nogroup" "--column" "--smart-case" "--stats" "--vimgrep" "--")
          ag-highlight-search t)

    (global-set-key (kbd "C-x C-a") 'ag-project)
  #+END_SRC

* arduino-mode

  #+BEGIN_SRC emacs-lisp
    (require 'arduino-mode)
    (require 'compile)

    (defun k20e/arduino-recompile ()
      "Recompile the project without messing with the windows."
      (interactive)
      (save-window-excursion (recompile)))

    (defun k20e/arduino-mode-hook ()
      (define-key arduino-mode-map (kbd "C-c C-c") 'k20e/arduino-recompile))

    (add-hook 'arduino-mode-hook 'k20e/arduino-mode-hook)
  #+END_SRC

* auto-fill

  When to turn on auto-fill and set fill-column to a reasonable value.  This
  would probably be better dealt with by a data structure that maps mode hooks
  to fill-column values.

  #+BEGIN_SRC emacs-lisp
    (defun k20e/auto-fill-mode-hook ()
      (setq fill-column 78))

    (add-hook 'auto-fill-mode 'k20e/auto-fill-mode-hook)
  #+END_SRC

* auto-save

  Disable =auto-save=.

  #+BEGIN_SRC emacs-lisp
    (setq auto-save-default nil
          auto-save-timeout 0)
  #+END_SRC

* buffer-move

  Move the current buffer up/down/left/right easily.

  #+BEGIN_SRC emacs-lisp
    (require 'buffer-move)

    (global-set-key (kbd "<H-S-up>") 'buf-move-up)
    (global-set-key (kbd "<H-S-down>") 'buf-move-down)
    (global-set-key (kbd "<H-S-left>") 'buf-move-left)
    (global-set-key (kbd "<H-S-right>") 'buf-move-right)
  #+END_SRC

* TODO calendar

  Does setting these geolocation variables in a hook really make sense since
  they are the result of an asynchronous query and response parsing?

  #+BEGIN_SRC emacs-lisp
    (require 'geo-ip)
    (require 'url)

    (defun k20e/calendar-load-hook ()
      ;; Default location Philly.
      (setq
       calendar-latitude 39.9            ; 39.9525
       calendar-longitude -75.1          ; -75.163
       calendar-location-name "Philadelphia, PA")

      ;; Attempt to set location with a geo-ip query.
      (geo-ip-lat-lon-loc-ip
       #'(lambda (lat lon loc ip)
           (setq
            calendar-latitude lat
            calendar-longitude lon
            calendar-location-name loc))))

    (add-hook 'calendar-load-hook 'k20e/calendar-load-hook)
  #+END_SRC

* cider

  #+BEGIN_SRC emacs-lisp
    (require 'cider)
    (require 'eldoc)
    (require 'paredit)

    (defun k20e/cider-mode-hook ()
      (eldoc-mode)
      (paredit-mode 1))

    (add-hook 'cider-mode-hook 'k20e/cider-mode-hook)

    (setq nrepl-hide-special-buffers t
          cider-show-error-buffer nil
          cider-repl-use-pretty-printing nil
          cider-repl-history-file (expand-file-name "nrepl-history" k20e/google-drive-directory))
  #+END_SRC

* clock-face

  This is a [[https://github.com/krismolendyke/clock-face.el][ridiculous package]] that I wrote to insert a Unicode clock
  face character for the nearest current half-hour.  üïô

  #+BEGIN_SRC emacs-lisp
    (require 'clock-face)
  #+END_SRC

* clojure-mode

  #+BEGIN_SRC emacs-lisp
    (require 'clojure-mode)
    (require 'cider-test)
    (require 'paredit)

    (defun k20e/clojure-mode-hook ()
      (paredit-mode 1))

    (add-hook 'clojure-mode-hook 'k20e/clojure-mode-hook)
  #+END_SRC

** Save buffers before loading or running tests

   Courtesy of Magnar Sveen's [[https://github.com/magnars/.emacs.d/blob/486e631801c84b018d90cf040d2170ef78045676/setup-clojure-mode.el][=setup-clojure-mode.el=]].

   #+BEGIN_SRC emacs-lisp
     (require 'cider-mode)
     (require 'cider-test)

     ;; (defadvice clojure-test-run-tests (before save-first activate)
     ;;   (save-buffer))

     ;; (defadvice cider-load-current-buffer (before save-first activate)
     ;;   (save-buffer))
   #+END_SRC

* compilation-mode

  #+BEGIN_SRC emacs-lisp
    (defun k20e/compilation-mode-hook ()
      (set-face-foreground 'compilation-error "tomato1"))

    (add-hook 'compilation-mode-hook 'k20e/compilation-mode-hook)
  #+END_SRC

  Functions to execute after compilation has finished:

  #+BEGIN_SRC emacs-lisp
    (require 'hl-line)
    (require 'subr-x)

    (defun k20e/compilation-finish-function-delay-delete (buf result)
      "Delete and bury BUF after short delay.
    Do so only if compilation is successful."
      (if (string= (string-trim result) "finished")
          (run-with-timer
           1.0 nil
           (lambda (buf)
             (with-current-buffer buf
               (delete-window)
               (bury-buffer)))
           buf)))

    (defun k20e/compilation-finish-function-select-window (buf result)
      "Switch to the compilation buffer BUF.
    When compilation completes, regardless of result."
      (let ((win (get-buffer-window buf)))
        (select-window (get-buffer-window buf))
        (goto-char (point-max))
        (forward-line -1)
        (hl-line-mode)))
  #+END_SRC

* dash-at-point

  [[https://kapeli.com/dash][Dash]] offline API access.

  #+BEGIN_SRC emacs-lisp
    (autoload 'dash-at-point
      "dash-at-point" "Search the word at point with Dash." t nil)

    (global-set-key (kbd "H-d") 'dash-at-point)
  #+END_SRC

* dired

  #+BEGIN_SRC emacs-lisp
    (require 'dired-x)
    (require 'ido)
    (require 'autorevert)

    (defun k20e/dired-load-hook ()
      (load "dired-x"))

    (add-hook 'dired-load-hook 'k20e/dired-load-hook)

    (defun k20e/dired-mode-hook ()
      (auto-revert-mode 1)
      (setq auto-revert-verbose nil)
      (set-face-foreground 'dired-flagged "tomato1")
      (set-face-attribute 'dired-flagged nil :strike-through t))

    (add-hook 'dired-mode-hook 'k20e/dired-mode-hook)

    ;; C-x C-d is normally bound to `ido-list-directory' which I rarely need and
    ;; often type when I intend to run `ido-dired'.
    (global-set-key (kbd "C-x C-d") 'ido-dired)
  #+END_SRC

* electric-pair-mode

  #+BEGIN_SRC emacs-lisp
    (require 'elec-pair)

    (electric-pair-mode t)
  #+END_SRC

* emacs-lisp-mode

  #+BEGIN_SRC emacs-lisp
    (defun k20e/emacs-lisp-mode-hook ()
      (eldoc-mode))

    (add-hook 'emacs-lisp-mode-hook 'k20e/emacs-lisp-mode-hook)
  #+END_SRC

* ert

  Emacs Lisp [[http://en.wikipedia.org/wiki/Unit_testing][unit testing]]!

  #+BEGIN_SRC emacs-lisp
    (require 'ert)

    (defun k20e/ert ()
      "Run all the tests in the universe!"
      (interactive)
      (ert t))

    (define-key emacs-lisp-mode-map (kbd "H-t") 'k20e/ert)
  #+END_SRC

* expand-region

  #+BEGIN_SRC emacs-lisp
    (require 'expand-region)

    (global-set-key (kbd "C-M-SPC") 'er/expand-region)
  #+END_SRC

* find-file-in-project

  #+BEGIN_SRC emacs-lisp
    (require 'find-file-in-project)

    (setq-default ffip-limit 8192
                  ffip-find-options "-not -regex \".*/build.*\""
                  ffip-full-paths t
                  ffip-patterns (list "*.clj"
                                      "*.conf"
                                      "*.cron"
                                      "*.css"
                                      "*.el"
                                      "*.html"
                                      "*.j2"
                                      "*.js"
                                      "*.json"
                                      "*.mk"
                                      "*.md"
                                      "*.org"
                                      "*.py"
                                      "*.rb"
                                      "*.rst"
                                      "*.sh"
                                      "*.soy"
                                      "*.txt"
                                      "*.yml"
                                      "Makefile")
                  ffip-prune-patterns (list ".git" "build"))

    (global-set-key (kbd "C-x o") 'find-file-in-project)
  #+END_SRC

* flycheck
  :PROPERTIES:
  :CUSTOM_ID: flycheck
  :END:

  #+BEGIN_SRC emacs-lisp
    (require 'flycheck)

    (setq-default flycheck-pylintrc "pylintrc"
                  flycheck-check-syntax-automatically '(mode-enabled save))
  #+END_SRC

* flyspell

  Setup =ispell= to use [[#install-aspell][=aspell=]]:

  #+BEGIN_SRC emacs-lisp
    (setq-default ispell-program-name "aspell"
                  ispell-extra-args (list "--sug-mode=ultra"))
  #+END_SRC

  Then setup =flyspell= itself.  It requires  =ispell=.

  #+BEGIN_SRC emacs-lisp
    (require 'flyspell)

    ;; When to turn on flyspell-mode.
    (dolist (hook '(text-mode-hook))
      (add-hook hook 'turn-on-flyspell))

    ;; When to turn on flyspell-prog-mode for comments and strings in source.
    ;; (dolist (hook '(emacs-lisp-mode-hook
    ;;                 lisp-mode-hook))
    ;;   (add-hook hook #'(lambda () (flyspell-prog-mode))))

    ;; Do not emit to *Messages*.
    (setq flyspell-issue-message-flag nil
          flyspell-issue-welcome-flag nil)
  #+END_SRC

** Install [[http://hunspell.sourceforge.net/][=aspell=]]
   :PROPERTIES:
   :CUSTOM_ID: install-aspell
   :END:

   Install =aspell= via Homebrew:

   #+BEGIN_SRC sh
     brew install aspell --with-lang-en
   #+END_SRC

* Fonts

  Managing fonts always seems to be a hassle.  These functions help to
  interactively set the font family and a preferred size from a short
  list of fonts that I like.

  #+BEGIN_SRC emacs-lisp
    (defvar k20e/font-list '(("Hack" . 11)
                             ("Source-Code-Pro" . 11)
                             ("Glass-TTY-VT220" . 20)
                             ("Consolas" . 18)
                             ("Ubuntu-Mono" . 17)
                             ("Inconsolata" . 18)
                             ("DejaVu-Sans-Mono" . 18)
                             ("PICO-8" . 20))
      "Ordered list of preferred fonts and sizes.")

    (defun k20e/font--set (font-alist)
      "Set the font family and size to the given font alist of the
    format (family . point)."
      (let ((font (replace-regexp-in-string "-" " " (car font-alist)))
            (height (* 10 (cdr font-alist))))
        (set-frame-font font)
        (set-face-attribute 'default nil :height height)))

    (defun k20e/font-set-from-list (l)
      "Set the font to first available font alist in the given list."
      (if (null l) nil
        (k20e/font--set (car l))
        (if (string= (replace-regexp-in-string "-" " "(caar l))
                     (face-attribute 'default :family (selected-frame)))
            (caar l)
          (k20e/font-set-from-list (cdr l)))))

    (defun k20e/font-set ()
      "Set a font from the `k20e/font-list'."
      (interactive)
      (let ((ignore-case completion-ignore-case))
        (unwind-protect
            (progn
              (setq completion-ignore-case t)
              (let ((font (completing-read "Font: " k20e/font-list)))
                (k20e/font--set (assoc font k20e/font-list))))
          (setq completion-ignore-case ignore-case))))

    (k20e/font-set-from-list k20e/font-list)
  #+END_SRC

** Unicode

   [[http://users.teilar.gr/~g1951d/][Symbola]] is a nice font for displaying Unicode characters üç∫üëç.

   #+BEGIN_SRC emacs-lisp
     (when (member "Symbola" (font-family-list))
       (set-fontset-font t 'unicode "Symbola" nil 'prepend))
   #+END_SRC

* font-awesome

  This is a [[https://github.com/krismolendyke/font-awesome.el][na√Øve package]] that I wrote to help insert [[http://fortawesome.github.io/Font-Awesome/][Font Awesome]]
  icons into buffers.

  #+BEGIN_SRC emacs-lisp
    (require 'font-awesome)
  #+END_SRC

* geiser

  #+BEGIN_SRC emacs-lisp
    (setq-default geiser-active-implementations '(racket chicken))
  #+END_SRC

* git

  #+BEGIN_SRC emacs-lisp
    (require 'gitconfig-mode)
    (require 'gitignore-mode)

    (autoload 'git-blame-mode "git-blame"
      "Minor mode for incremental blame for Git." t)

    (global-set-key (kbd "C-x v b") 'git-blame-mode)
  #+END_SRC

** GitHub =.gitignore=

   A simple function to insert starter =.gitignore= file contents from
   the [[https://github.com/github/gitignore][github/gitignore]] repository.

   #+BEGIN_SRC emacs-lisp
     (require 'url)

     (defun k20e/gh--gitignore-url (language)
       "Get GitHub .gitignore URL for LANGUAGE."
       (format "https://raw.githubusercontent.com/github/gitignore/master/%s.gitignore"
               (capitalize language)))

     (defun k20e/gh--gitignore-get-region (response-buffer)
       "Get GitHub .gitignore response body bounds.
     Argument RESPONSE-BUFFER HTTP GET response."
       (with-current-buffer response-buffer
         (goto-char (point-min))
         (let ((start (1+ (search-forward-regexp "^$")))
               (end (point-max)))
           (list start end))))

     (defun k20e/gh-gitignore-insert (language)
       "Insert Github .gitignore for LANGUAGE."
       (interactive "sLanguage: ")
       (let* ((response-buffer (url-retrieve-synchronously
                                (k20e/gh--gitignore-url language) t))
              (gitignore-region (k20e/gh--gitignore-get-region response-buffer)))
         (insert-buffer-substring-no-properties
          response-buffer (car gitignore-region) (cadr gitignore-region))))
   #+END_SRC

* go-mode

  Install [[https://github.com/golang/lint][golint]]:

  #+BEGIN_SRC sh
     go get -u github.com/golang/lint/golint
  #+END_SRC

  #+BEGIN_SRC emacs-lisp
    (require 'flycheck)
    (require 'go-mode)

    (defun k20e/go-mode-hook ()
      (let ((gopath (getenv "GOPATH"))
            (d "src/github.com/golang/lint/misc/emacs")
            (f "golint.el"))
        (require 'golint (expand-file-name f (expand-file-name d gopath))))
      (setq tab-width 4
            indent-tabs-mode t)
      (flycheck-mode 1)
      (add-hook 'before-save-hook 'gofmt-before-save))

    (define-key go-mode-map (kbd "C-c C-t") 'k20e/toggle-test-buffer)
    (define-key go-mode-map (kbd "C-c C-r") 'go-remove-unused-imports)

    (add-hook 'go-mode-hook 'k20e/go-mode-hook)
  #+END_SRC

* groovy-mode

  #+BEGIN_SRC emacs-lisp
    (require 'cc-vars)

    (defun k20e/groovy-mode-hook ()
      (setq c-basic-offset 4))

    (add-hook 'groovy-mode-hook 'k20e/groovy-mode-hook)
  #+END_SRC

* highlight-parentheses

  #+BEGIN_SRC emacs-lisp
    (require 'highlight-parentheses)

    (dolist (hook '(emacs-lisp-mode-hook
                    lisp-mode-hook
                    cider-repl-mode-hook
                    clojure-mode-hook))
      (add-hook hook #'(lambda ()
                         (highlight-parentheses-mode))))
  #+END_SRC

* hyperspec

  #+BEGIN_SRC emacs-lisp
    ;; Set HyperSpec root in Google Drive.
    (defvar common-lisp-hyperspec-root
      (format "file://%s/"
              (expand-file-name "Documents/HyperSpec" k20e/google-drive-directory)))
  #+END_SRC

* ibuffer

  #+BEGIN_SRC emacs-lisp
    (require 'ibuffer)
    (require 'ido)

    (defalias 'list-buffers 'ibuffer)

    (setq ibuffer-formats
          '((mark " "
                  (modified)
                  " "
                  (name 40 40 :right :elide)
                  " "
                  (filename-and-process))
            (mark " "
                  (filename-and-process 70 70 :left :elide)
                  " "
                  name)))

    (defun k20e/ibuffer-hook ()
      (define-key ibuffer-mode-map (kbd "C-x C-f") 'ido-find-file))

    (add-hook 'ibuffer-hook 'k20e/ibuffer-hook)
  #+END_SRC

* ido

  #+BEGIN_SRC emacs-lisp
    (require 'flx-ido)
    (require 'ido)
    (require 'ido-vertical-mode)

    (setq ido-save-directory-list-file (expand-file-name ".ido.last" k20e/google-drive-directory)
          ido-enable-flex-matching t
          ido-auto-merge-work-directories-length -1
          ido-create-new-buffer 'always
          ido-show-dot-for-dired t
          ido-max-file-prompt-width 0.2
          ido-use-faces t
          flx-ido-use-faces t)

    (add-to-list 'ido-ignore-files "\\.DS_Store")
  #+END_SRC

  Avoid =ido-vertical-mode= from eating =M-p=.

  #+BEGIN_SRC emacs-lisp
    (setq ido-vertical-define-keys nil)
  #+END_SRC

** Interface

   #+BEGIN_SRC emacs-lisp
     (setq ido-vertical-decorations '("\n"  ; left bracket around prospect list
                                      ""    ; right bracket around prospect list
                                      "\n"  ; separator between prospects, depends on `ido-separator`
                                      "\n‚ñº" ; inserted at the end of a truncated list of prospects
                                      "["   ; left bracket around common match string
                                      "]"   ; right bracket around common match string
                                      " ‚úò"  ; no match
                                      " ‚úî"  ; matched
                                      " [Not readable]"
                                      " [Too big]"
                                      " ?"  ; confirm
                                      "\n"  ; left bracket around the sole remaining completion
                                      " ‚úî"  ; right bracket around the sole remaining completion
                                      ))
   #+END_SRC

** Minibuffer

   Scale up the minibuffer text size and limit how tall it can get.

   #+BEGIN_SRC emacs-lisp
     (defun k20e/minibuffer-setup-hook ()
       "Bump up minibuffer text size and height."
       (text-scale-set 3)
       (setq max-mini-window-height 20))

     (add-hook 'ido-minibuffer-setup-hook 'k20e/minibuffer-setup-hook)
     (add-hook 'minibuffer-setup-hook 'k20e/minibuffer-setup-hook)
   #+END_SRC

** Keys and Theme

   #+BEGIN_SRC emacs-lisp
     (defun k20e/ido-setup-hook ()
       "Setup key map and theme faces."
       (define-key ido-completion-map (kbd "C-n") 'ido-next-match)
       (define-key ido-completion-map (kbd "C-p") 'ido-prev-match)
       (define-key ido-completion-map (kbd "<up>") 'ido-prev-match)
       (define-key ido-completion-map (kbd "<down>") 'ido-next-match)
       (define-key ido-completion-map (kbd "<left>") 'ido-vertical-prev-match)
       (define-key ido-completion-map (kbd "<right>") 'ido-vertical-next-match)

       (define-key ido-completion-map (kbd "C-h") 'delete-backward-char)

       ;; Theme!
       (let ((match (face-attribute 'font-lock-string-face :foreground))
             (highlight (face-attribute 'font-lock-keyword-face :foreground)))
         (custom-set-faces `(ido-first-match ((t (:foreground ,match))))
                           `(ido-only-match ((t (:foreground ,match))))
                           `(flx-highlight-face ((t (:foreground ,highlight
                                                     :underline nil)))))))

     (add-hook 'ido-setup-hook 'k20e/ido-setup-hook)
   #+END_SRC

** Enable

   Vertical, everywhere, fuzzy matching.

   #+BEGIN_SRC emacs-lisp
     (ido-mode t)
     (ido-vertical-mode t)
     (ido-ubiquitous-mode t)
     (flx-ido-mode t)
   #+END_SRC

* IELM

  #+BEGIN_SRC emacs-lisp
    (require 'eldoc)
    (require 'paredit)

    (defun k20e/ielm-hook ()
      (eldoc-mode)
      (paredit-mode 1))

    (add-hook 'ielm-mode-hook 'k20e/ielm-hook)
  #+END_SRC

* imenu

  Re-scan the buffer for new menu items automatically.

  #+BEGIN_SRC emacs-lisp
    (setq imenu-auto-rescan t)
  #+END_SRC

* I'm Feeling Lucky

  This is [[https://github.com/krismolendyke/im-feeling-lucky.el][my Google search]] module.

  #+BEGIN_SRC emacs-lisp
    (require 'im-feeling-lucky)

    (global-set-key (kbd "H-l") 'ifl-region-or-query)
  #+END_SRC

* isearch

  #+BEGIN_SRC emacs-lisp
    (eval-after-load "isearch"
      '(define-key isearch-mode-map (kbd "C-h") 'isearch-delete-char))
  #+END_SRC

* js-mode

  #+BEGIN_SRC emacs-lisp
    (require 'flycheck)
    (require 'json)

    (defun k20e/js-mode-hook ()
      (setq-default flycheck-gjslintrc "gjslintrc")
      (flycheck-mode 1))

    (add-hook 'js-mode-hook 'k20e/js-mode-hook)

    (add-to-list 'auto-mode-alist '("\\.json" . js-mode))
  #+END_SRC

* keyfreq

  #+BEGIN_SRC emacs-lisp
    (require 'keyfreq)

    (setq keyfreq-file (expand-file-name ".emacs-keyfreq" k20e/google-drive-directory)
          keyfreq-file-lock (expand-file-name ".emacs-keyfreq-lock" k20e/google-drive-directory))

    (keyfreq-mode 1)
    (keyfreq-autosave-mode 1)
  #+END_SRC

* LaTeX

  #+BEGIN_SRC emacs-lisp
    (require 'tex-mode)

    (define-key latex-mode-map (kbd "C-j") 'join-line)
  #+END_SRC

* lockfiles

  [[http://stackoverflow.com/questions/5738170/why-does-emacs-create-temporary-symbolic-links-for-modified-files][Avoid creating temporary symbolic links]] and disturbing working
  directory state at the expense of avoiding editing collisions that I
  do not ever anticipate.

  #+BEGIN_SRC emacs-lisp
    (setq create-lockfiles nil)
  #+END_SRC

* man

  Setting a width avoids a possibly (likely) poorly chosen automatic
  width.

  #+BEGIN_SRC emacs-lisp
    (setq-default Man-width 80)
  #+END_SRC

* markdown-mode

  #+BEGIN_SRC emacs-lisp
    (require 'markdown-mode)

    (defun k20e/markdown-mode-hook ()
      (setq markdown-open-command "open"))

    (add-hook 'markdown-mode-hook 'k20e/markdown-mode-hook)
  #+END_SRC

* minibuffers

  Set =enable-recursive-minibufers= to =t= to allow minibuffers
  /within/ minibuffers.  A good use-case of this feature is described
  in [[http://www.masteringemacs.org/articles/2011/10/19/executing-shell-commands-emacs/][Executing Shell Commands in Emacs]].

  #+BEGIN_SRC emacs-lisp
    (setq enable-recursive-minibuffers t)
  #+END_SRC

** Eval expression minibuffer

   Enable =eldoc= in the modeline.

   #+BEGIN_SRC emacs-lisp
     (require 'eldoc)

     (defun k20e/eval-expression-minibuffer-setup-hook ()
       (eldoc-mode 1))

     (add-hook 'eval-expression-minibuffer-setup-hook
               'k20e/eval-expression-minibuffer-setup-hook)
   #+END_SRC

* monetate-mode

  #+BEGIN_SRC emacs-lisp
    (require 'monetate-mode)
    (require 'terminal-notifier)

    (defun k20e/monetate-hook ()
      "Enable monetate-mode if file is in a Monetate repository."
      (if (monetate-repo-p) (monetate-mode 1)))

    (dolist (hook '(python-mode-hook js-mode-hook shell-mode-hook sql-mode))
      (add-hook hook 'k20e/monetate-hook))

    (setq monetate-notify-function 'monetate--notify-terminal-notifier
          monetate-notify-process-messages '((started . "üïê Started‚Ä¶")
                                             (success . "‚úÖ Success!")
                                             (failure . "üö´ Error")))

  #+END_SRC

* multi-term
  :PROPERTIES:
  :CUSTOM_ID: multi-term
  :END:

  Together with [[#term][term]] this sets up my terminal environment within Emacs.

  =multi-term= adds a nice shortcut for flipping between only terminal
  buffers.  It also lets me fix a big annoyance by binding =M-h= to
  =backward-kill-word= easily.

  #+BEGIN_SRC emacs-lisp
    (require 'term)

    (defun k20e/term-toggle-mode ()
      "Toggle between `term-line-mode' and `term-char-mode'."
      (interactive)
      (if (term-in-char-mode)
          (term-line-mode)
        (term-char-mode)))
  #+END_SRC

  #+BEGIN_SRC emacs-lisp
    (require 'multi-term)

    (defcustom term-bind-key-alist
        '(("C-c C-c" . term-interrupt-subjob)
          ("C-h" . term-send-backspace)
          ("C-c C-j" . k20e/term-toggle-mode)
          ("C-c C-k" . k20e/term-toggle-mode)
          ("C-p" . term-send-up)
          ("C-n" . term-send-down)
          ("C-r" . term-send-reverse-search-history)
          ("C-m" . term-send-raw)
          ("C-y" . term-send-raw)
          ("C-z" . term-stop-subjob)
          ("M-f" . term-send-forward-word)
          ("M-b" . term-send-backward-word)
          ("M-p" . previous-line)
          ("M-n" . next-line)
          ("M-d" . term-send-forward-kill-word)
          ("M-h" . term-send-backward-kill-word)
          ("M-r" . isearch-backward)
          ("M-s" . isearch-forward)
          ("M-." . completion-at-point)
          ("M-]" . multi-term-next)
          ("M-[" . multi-term-prev))
        "Custom key bindings for `multi-term'."
        :type 'alist
        :group 'multi-term)

    (defun k20e/multi-term-hook ()
      "Re-evaluate my custom key bindings."
      (custom-reevaluate-setting 'term-bind-key-alist))

    (add-hook 'term-mode-hook 'k20e/multi-term-hook)

    (defalias 'zsh 'multi-term
      "Execute `multi-term' when `zsh' is executed.
    `multi-term' will look at the environment $SHELL value to
    determine the shell to run.  I have it set to zsh.")
  #+END_SRC

  =k20e/multi-term-hook= is necessary to re-evaluate my custom key bindings
  after =multi-term= is loaded.  Otherwise it overrides my bindings with its
  bindings whenever I open a new terminal.

** Global Key Bindings

   #+BEGIN_SRC emacs-lisp
     (global-set-key (kbd "<f2>") 'multi-term)
     (global-set-key (kbd "<f11>") 'multi-term-next)
   #+END_SRC

* multiple-cursors

  #+BEGIN_SRC emacs-lisp
    (require 'multiple-cursors)
  #+END_SRC

  #+BEGIN_SRC emacs-lisp
    (defun k20e/mark-next (extended)
      "Wrap multiple-cursors mark-more/next.
    Call `mc/mark-next-like-this' without a prefix argument.
    Argument EXTENDED Prefix argument to call function `mc/mark-more-like-this-extended'."
      (interactive "P")
      (if extended
          (call-interactively 'mc/mark-more-like-this-extended)
        (call-interactively 'mc/mark-next-like-this)))

    (defun k20e/mark-previous (extended)
      "Wrap multiple-cursors mark-more/previous.
    Call `mc/mark-previous-like-this' without a prefix argument.
    Argument EXTENDED Prefix argument to call function `mc/mark-more-like-this-extended'."
      (interactive "P")
      (if extended
          (call-interactively 'mc/mark-more-like-this-extended)
        (call-interactively 'mc/mark-previous-like-this)))
  #+END_SRC

  Setup key bindings:

  #+BEGIN_SRC emacs-lisp
    (global-set-key (kbd "M-L") 'mc/edit-lines)
    (global-set-key (kbd "C-M-.") 'k20e/mark-next)
    (global-set-key (kbd "C-M-,") 'k20e/mark-previous)
    (global-set-key (kbd "C-M-<return>") 'mc/mark-all-like-this)
  #+END_SRC

  Keep preferences sync'd across machines.

  #+BEGIN_SRC emacs-lisp
    (setq mc/list-file (expand-file-name ".mc-lists.el" k20e/google-drive-directory))
  #+END_SRC

* Open Source Licenses

  #+BEGIN_SRC emacs-lisp
    (defun k20e/insert-mit-license ()
      "Insert MIT license file contents.
    Populate the current year and user name."
      (interactive)
      (with-current-buffer (get-buffer-create "LICENSE.txt")
        (insert (format "The MIT License (MIT)

    Copyright (c) %s %s

    Permission is hereby granted, free of charge, to any person obtaining a copy
    of this software and associated documentation files (the \"Software\"), to deal
    in the Software without restriction, including without limitation the rights
    to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
    copies of the Software, and to permit persons to whom the Software is
    furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be included in
    all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED \"AS IS\", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
    IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
    FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
    AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
    LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
    OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
    THE SOFTWARE.
    " (format-time-string "%Y") (user-full-name)))))
  #+END_SRC

* org-mode

  #+BEGIN_SRC emacs-lisp
    (require 'org)

    (defun k20e/org-return ()
      "Tell `org-return' to indent, please."
      (interactive)
      (org-return t))

    (defun k20e/org-mode-hook ()
      (auto-fill-mode 1)
      (org-toggle-pretty-entities) ;; Display entities as UTF-8 characters.
      (visual-line-mode 0)
      (setq truncate-lines nil))

    (add-hook 'org-mode-hook 'k20e/org-mode-hook)

    ;; Set the org directory.
    (setq org-directory (expand-file-name "org" k20e/google-drive-directory))

    ;; Speeeeeeeeeed!  Move to very beginning of a headline and press "?"
    (setq org-use-speed-commands t)

    ;; "Special" `C-a' and `C-e' movement in headlines.
    (setq org-special-ctrl-a/e t)

    ;; Use completion in the current buffer for movement.
    (setq org-goto-interface 'outline-path-completion)

    ;; org-capture.
    (setq org-default-notes-file (expand-file-name "notes.org" org-directory))

    ;; Global key binding to make storing links to files easier.
    (global-set-key (kbd "C-c l") 'org-store-link)

    ;; Use ido-completion.
    (setq-default org-completion-use-ido t)

    ;; Now that ido-completion is enabled, use it when jumping around.
    (setq org-outline-path-complete-in-steps nil)

    (setq org-ellipsis nil)
  #+END_SRC

** Inline Images

   Try to get the width of images displayed inline from a =#+ATTR.*=
   keyword, e.g., =#+ATTR_HTML: :width 800px=, fall back to original
   image width if no attribute keyword is found:

   #+BEGIN_SRC emacs-lisp
     (setq org-image-actual-width nil)
   #+END_SRC

** Key Bindings

   #+BEGIN_SRC emacs-lisp
     (global-set-key (kbd "C-c a") 'org-agenda)
     (global-set-key (kbd "C-x c") 'org-switchb)
     (global-set-key (kbd "<f12>") 'org-agenda-list)

     (define-key org-mode-map (kbd "<return>") 'k20e/org-return)
     (define-key org-mode-map (kbd "C-j") 'join-line)
     (define-key org-mode-map (kbd "C-m") 'k20e/org-return)
     (define-key org-mode-map (kbd "H-<tab>") 'pcomplete)
     (define-key org-mode-map (kbd "M-h") 'backward-kill-word)
   #+END_SRC

** Export

   Most non-interactive export settings are defined in [[https://github.com/krismolendyke/.emacs.d/blob/master/elisp/k20e-org-html-export.el][a file loaded
   during initialization]].  Those settings are defined during
   initialization time to support a fast batch process for exporting
   /this/ document to HTML in a Git =post-commit= hook.

   #+BEGIN_SRC emacs-lisp
     (require 'k20e-org-html-export)
   #+END_SRC

   Interactive customization can be done here.

   #+BEGIN_SRC emacs-lisp
     (require 'ox-publish)

     ;; Enable "expert" export interface.
     (setq org-export-dispatch-use-expert-ui t)
   #+END_SRC

*** Options

    A message in =*Messages*= like:

    #+BEGIN_EXAMPLE
      user-error: Unable to resolve link: nil
    #+END_EXAMPLE

    indicates that a link somewhere is malformed.  Adding the option:

    #+BEGIN_SRC org
      ,#+OPTIONS: broken-links:mark
    #+END_SRC

    and exporting will insert =BROKEN= into the HTML document.
    Searching for that token makes finding the offending broken link
    much easier.  Keeping this option set all the time would let
    broken links slip through the export process undetected.

*** Backends

    #+BEGIN_SRC emacs-lisp
      (require 'ox-md)

      (add-to-list 'org-export-backends 'md)
    #+END_SRC

** Publish

   #+BEGIN_SRC emacs-lisp
     (setq org-publish-project-alist
           `(("k20e.com-org-files"
              :base-directory ,(expand-file-name "source" (expand-file-name "k20e.com" k20e/google-drive-directory))
              :base-extension "org"
              :recursive t
              :exclude "ga.org\\|level-0.org\\|todo.org\\|.DS_Store"
              :publishing-directory ,(expand-file-name "published" (expand-file-name "k20e.com" k20e/google-drive-directory))
              :publishing-function org-html-publish-to-html
              :with-planning t)
             ("k20e.com-static-files"
              :base-directory ,(expand-file-name "source" (expand-file-name "k20e.com" k20e/google-drive-directory))
              :base-extension "jpg\\|png\\|ico"
              :recursive t
              :publishing-directory ,(expand-file-name "published" (expand-file-name "k20e.com" k20e/google-drive-directory))
              :publishing-function org-publish-attachment)
             ("k20e.com"
              :components ("k20e.com-org-files" "k20e.com-static-files"))
             ("work-org-files"
              :base-directory ,(expand-file-name "work" org-directory)
              :base-extension "org"
              :publishing-directory ,(expand-file-name "published" (expand-file-name "work" org-directory))
              :publishing-function org-html-publish-to-html
              :with-planning t)
             ("work-static-files"
              :base-directory ,(expand-file-name "work" org-directory)
              :base-extension "pdf\\|csv\\|sql\\|png"
              :publishing-directory ,(expand-file-name "published" (expand-file-name "work" org-directory))
              :publishing-function org-publish-attachment)
             ("work"
              :components ("work-org-files" "work-static-files"))
             ("house-org-files"
              :base-directory ,(expand-file-name "house" org-directory)
              :base-extension "org"
              :recursive t
              :publishing-directory ,(expand-file-name "published" (expand-file-name "house" org-directory))
              :publishing-function org-html-publish-to-html
              :with-planning t)
             ("house-static-files"
              :base-directory ,(expand-file-name "house" org-directory)
              :base-extension "pdf\\|csv\\|png\\|xls\\|doc"
              :recursive t
              :publishing-directory ,(expand-file-name "published" (expand-file-name "house" org-directory))
              :publishing-function org-publish-attachment)
             ("house"
              :components ("house-org-files" "house-static-files"))))
   #+END_SRC

** Babel

   Define [[http://orgmode.org/worg/org-contrib/babel/languages.html][which languages]] =org-babel= should support.

   #+BEGIN_SRC emacs-lisp
     (defvar k20e/org-babel-load-languages
       '((ditaa . t)
         (emacs-lisp . t)
         (org . t)
         (python . t)
         (shell . t))
       "Languages to evaluate in `org-mode'.")

     (org-babel-do-load-languages 'org-babel-load-languages
                                  k20e/org-babel-load-languages)
   #+END_SRC

** TODO Items

   Automatically insert a timestamp when a task is marked =DONE=.

   #+BEGIN_SRC emacs-lisp
     (setq org-log-done t)
   #+END_SRC

   Custom keywords and faces.

   #+BEGIN_SRC emacs-lisp
     (setq org-todo-keywords '((sequence
                                "TODO(t)"
                                "STARTED(s/!)"
                                "|"
                                "DONE(d!)"
                                "CANCELED(c@)"))
           org-todo-keyword-faces '(("TODO" . org-todo)
                                    ("STARTED" . org-code)
                                    ("CANCELED" . org-ellipsis)
                                    ("DONE" . org-done)))
   #+END_SRC

** Agenda

   #+BEGIN_SRC emacs-lisp
     (require 'face-remap)
     (require 'org)
     (require 'org-agenda)
     (require 'winner)

     (defun k20e/org-agenda-mode-hook ()
       (define-key org-agenda-mode-map (kbd "q") 'winner-undo)
       (delete-other-windows)
       (text-scale-set 2))

     (add-hook 'org-agenda-mode-hook 'k20e/org-agenda-mode-hook)
   #+END_SRC

*** Files

   #+BEGIN_SRC emacs-lisp
     (setq org-agenda-files
           (list (expand-file-name "personal.org" org-directory)
                 (expand-file-name "work" org-directory)
                 (expand-file-name "chores.org" org-directory)))
   #+END_SRC

*** Deadlines

   Non-nil means skip scheduling line if same entry shows because of deadline.

   In the agenda of today, an entry can show up multiple times because it is
   both scheduled and has a nearby deadline, and maybe a plain time stamp as
   well.

   When set to t, then only the deadline is shown and the fact that the entry
   is scheduled today or was scheduled previously is not shown.

   #+BEGIN_SRC emacs-lisp
     (setq org-agenda-skip-scheduled-if-deadline-is-shown nil)
   #+END_SRC

*** List

   Default to showing only today in the agenda list.

   #+BEGIN_SRC emacs-lisp
     (setq org-agenda-span 'day)
   #+END_SRC

** Drill

   #+BEGIN_SRC emacs-lisp
     (require 'org-drill)
   #+END_SRC

** Habit

   #+BEGIN_SRC emacs-lisp
     (require 'org-habit)

     (setq org-habit-completed-glyph ?‚úì
           org-habit-today-glyph ?|)
   #+END_SRC

** Logging & Drawers

   Insert state change notes and time stamps into a drawer rather than simply
   "loose" after a headline.

   #+BEGIN_SRC emacs-lisp
     (setq org-log-into-drawer t)
   #+END_SRC
** Clock

   #+BEGIN_SRC emacs-lisp
     (defvar org-clock-idle-time 5)
   #+END_SRC

* paredit-mode

  #+BEGIN_SRC emacs-lisp
    (autoload 'paredit-mode "paredit" nil t)

    ;; When to turn on paredit.
    (dolist (hook '(cider-repl-mode-hook
                    emacs-lisp-mode-hook
                    geiser-mode-hook
                    geiser-repl-mode-hook
                    lisp-mode-hook
                    scheme-mode-hook))
      (add-hook hook #'(lambda nil (paredit-mode 1))))

    (eval-after-load "paredit"
      '(progn
         (define-key paredit-mode-map [?\)] 'paredit-close-parenthesis)
         (define-key paredit-mode-map [(meta ?\))]
           'paredit-close-parenthesis-and-newline)
         (define-key paredit-mode-map (kbd "C-h") 'paredit-backward-delete)
         (define-key paredit-mode-map (kbd "C-j") 'join-line)))
  #+END_SRC

* python

  #+BEGIN_SRC emacs-lisp
    (require 'electric)
    (require 'flycheck)
    (require 'multiple-cursors)
    (require 'python)
    (require 'yasnippet)

    (defun k20e/python-mode-hook ()
      (superword-mode)
      ;; Do not drive me crazy with extra-dumb indentation!
      (setq electric-indent-inhibit t)
      (linum-mode 0)
      (flycheck-mode 1)
      (setq fill-column 118)
      (yas-minor-mode 1)
      ;; Previously:
      ;; C-M-f, C-M-b (paredit-forward/back)
      ;; C-M-n, C-M-p (forward-list/backward-list)
      ;; C-M-a, C-M-e (beginning-of-defun/end-of-defun)
      (define-key python-mode-map (kbd "M-a") 'python-nav-beginning-of-statement)
      (define-key python-mode-map (kbd "M-e") 'python-nav-end-of-statement)
      (define-key python-mode-map (kbd "M-n") 'python-nav-forward-statement)
      (define-key python-mode-map (kbd "M-p") 'python-nav-backward-statement)
      (define-key python-mode-map (kbd "C-M-f") 'python-nav-forward-sexp)
      (define-key python-mode-map (kbd "C-M-b") 'python-nav-backward-sexp)
      (define-key python-mode-map (kbd "C-M-n") 'python-nav-forward-block)
      (define-key python-mode-map (kbd "C-M-p") 'python-nav-backward-block))

    (add-hook 'python-mode-hook 'k20e/python-mode-hook)
  #+END_SRC

** IPython

   #+BEGIN_SRC emacs-lisp
     (setq python-shell-interpreter "ipython"
           python-shell-interpreter-args "-i --no-banner")
   #+END_SRC

* recentf

  #+BEGIN_SRC emacs-lisp
    (require 'recentf)

    (setq recentf-save-file (expand-file-name ".recentf" k20e/google-drive-directory)
          recentf-max-saved-items 250)
    (recentf-mode 1)

    ;;; Adapted from http://emacsredux.com/blog/2013/04/05/recently-visited-files
    (defun recentf-ido-find-file ()
      "Find a recently opened file with ido."
      (interactive)
      (let ((file (ido-completing-read "Find recent file: " recentf-list nil t)))
        (if file (find-file file))))

    (global-set-key (kbd "C-x C-r") 'recentf-ido-find-file)
  #+END_SRC

* reStructuredText

  #+BEGIN_SRC emacs-lisp
    (require 'rst)

    (defun k20e/rst-mode-hook ()
      (setq fill-column 78)
      (set-default 'rst-preferred-adornments '((?= simple 0)
                                               (?- simple 0)
                                               (?~ simple 0)
                                               (?* simple 0)
                                               (?+ simple 0)
                                               (?# simple 0)
                                               (?@ simple 0))))

    (add-hook 'rst-mode-hook 'k20e/rst-mode-hook)
  #+END_SRC

* rust

  #+BEGIN_SRC emacs-lisp
    (require 'rust-mode)

    (defun k20e/rust-mode-hook ()
      (setq rust-format-on-save t))

    (add-hook 'rust-mode-hook 'k20e/rust-mode-hook)
  #+END_SRC

* savehist

  #+BEGIN_SRC emacs-lisp
    ;; Save minibuffer history.
    (require 'savehist)

    (setq savehist-file (expand-file-name ".savehist" k20e/google-drive-directory))
    (savehist-mode)
  #+END_SRC

* =*scratch*=

  Begin with an empty =*scratch*= file.

  #+BEGIN_SRC emacs-lisp
    (setq initial-scratch-message nil)
  #+END_SRC

  Set it to Emacs Lisp mode.

  #+BEGIN_SRC emacs-lisp
    (with-current-buffer (get-buffer-create "*scratch*")
      (emacs-lisp-mode))
  #+END_SRC

** Quickly create new scratch buffers

   With a preset list of major modes that I find often need scratch
   pads for.

   #+BEGIN_SRC emacs-lisp
     (require 'ido)

     (defconst k20e/scratch-buffer-modes
       '(fundamental-mode
         emacs-lisp-mode
         python-mode
         javascript-mode
         org-mode
         sql-mode
         text-mode)
       "Common major modes to create scratch buffers for.")

     (defun k20e/scratch-buffer ()
       "Generate a new scratch buffer.
     Choose from `k20e/scratch-buffer-modes' list of major modes to
     enable in the newly created scratch buffer and switch to it."
       (interactive)
       (let ((mode (read (ido-completing-read "New *scratch* buffer with mode: "
                                              (mapcar (lambda (el) (format "%s" el))
                                                      k20e/scratch-buffer-modes)))))
         (switch-to-buffer (generate-new-buffer (format "*scratch-%s*" mode)))
         (funcall mode)))
   #+END_SRC

   Bind it globally.

   #+BEGIN_SRC emacs-lisp
     (global-set-key (kbd "<f10>") 'k20e/scratch-buffer)
   #+END_SRC

* smex

  #+BEGIN_SRC emacs-lisp
    (require 'smex)
    (smex-initialize)

    ;; Replace execute-extended-command binding with smex.
    (global-set-key (kbd "M-x") 'smex)
    (global-set-key (kbd "M-X") 'smex-major-mode-commands)

    ;; Keep execute-extended-command at hand just in case.
    (global-set-key (kbd "C-c C-c M-x") 'execute-extended-command)

    ;; Share smex history across my machines.
    (setq smex-save-file (expand-file-name ".smex-items" k20e/google-drive-directory))
  #+END_SRC

* sql-mode

  #+BEGIN_SRC emacs-lisp
    (require 'sql)

    (defun k20e/sql-mode-hook ()
      (setq sql-product 'mysql)
      (sql-highlight-mysql-keywords))

    (add-hook 'sql-mode-hook 'k20e/sql-mode-hook)
  #+END_SRC

* server

  #+BEGIN_SRC emacs-lisp
    ;; Start the Emacs server.
    (require 'server)

    (unless (server-running-p)
      (server-start))
  #+END_SRC

* term
  :PROPERTIES:
  :CUSTOM_ID: term
  :END:

  Together with [[#multi-term][multi-term]] this sets up my terminal environment within Emacs.

** Key Bindings

   =term-raw-map= is used in /char/ mode.

   #+BEGIN_SRC emacs-lisp
     (require 'smex)

     (define-key term-raw-map (kbd "M-x") 'smex)
     (define-key term-raw-map (kbd "M-h") 'backward-kill-word)
   #+END_SRC

   =term-mode-map= is used in /line/ mode.

   #+BEGIN_SRC emacs-lisp
     (define-key term-mode-map (kbd "M-x") 'smex)
   #+END_SRC

* terminal-notifier

  This is a tiny package that I wrote to help with displaying
  notifications in OS X.

  #+BEGIN_SRC emacs-lisp
    (require 'terminal-notifier)
  #+END_SRC

* text-mode

  #+BEGIN_SRC emacs-lisp
    (defun k20e/text-mode-hook ()
      (auto-fill-mode 1))

    (add-hook 'text-mode-hook 'k20e/text-mode-hook)
  #+END_SRC

* tramp

** =ControlPath=

   Fix =ControlPath too long= errors due to OS X pitching a [[https://lists.macosforge.org/pipermail/macports-tickets/2011-June/084295.html][long
   temporary directory]] to =ssh=.

   Unfortunately, setting this is blowing up the =server-start= which
   can no longer find the socket stored in the original =TMPDIR=
   value.

   #+BEGIN_SRC emacs-lisp
     ;; (setenv "TMPDIR" "/tmp")
   #+END_SRC

   Eureka!  It appears that the =ControlMaster= option for =ssh=
   should be set to =yes= instead of =auto= to avoid the =ControlPath
   too long= error.  Here is the interesting section of =man 5
   ssh_config=:

   #+BEGIN_EXAMPLE
     ControlMaster
                  Enables the sharing of multiple sessions over a single network connection.  When set to
                  ``yes'', ssh(1) will listen for connections on a control socket specified using the
                  ControlPath argument.  Additional sessions can connect to this socket using the same
                  ControlPath with ControlMaster set to ``no'' (the default).  These sessions will try to reuse
                  the master instance's network connection rather than initiating new ones, but will fall back
                  to connecting normally if the control socket does not exist, or is not listening.

                  Setting this to ``ask'' will cause ssh to listen for control connections, but require confir-
                  mation using the SSH_ASKPASS program before they are accepted (see ssh-add(1) for details).
                  If the ControlPath cannot be opened, ssh will continue without connecting to a master
                  instance.

                  X11 and ssh-agent(1) forwarding is supported over these multiplexed connections, however the
                  display and agent forwarded will be the one belonging to the master connection i.e. it is not
                  possible to forward multiple displays or agents.

                  Two additional options allow for opportunistic multiplexing: try to use a master connection
                  but fall back to creating a new one if one does not already exist.  These options are:
                  ``auto'' and ``autoask''.  The latter requires confirmation like the ``ask'' option.
   #+END_EXAMPLE

   The =tramp-ssh-controlmaster-options= variable is responsible for
   the =ControlMaster= value as well as a few other options which have
   not been changed from their default values.

   #+BEGIN_SRC emacs-lisp
     (setq tramp-ssh-controlmaster-options
           "-o ControlPath=%t.%%r@%%h:%%p -o ControlMaster=yes -o ControlPersist=no")
   #+END_SRC

** Inline Copying

   Do not inline copy files.  This is to avoid =File exists, but
   cannot be read= errors.

   #+BEGIN_SRC emacs-lisp
     (setq-default tramp-copy-size-limit -1)
   #+END_SRC

** File Backup

   Do not backup files edited by =tramp= to [[http://www.gnu.org/software/emacs/manual/html_node/tramp/Auto_002dsave-and-Backup.html][avoid possibly sharing
   copies of privileged files with non-privileged users]].

   #+BEGIN_SRC emacs-lisp
     (add-to-list 'backup-directory-alist (list tramp-file-name-regexp))
   #+END_SRC

** Debugging

   #+BEGIN_SRC emacs-lisp
     ;; (setq tramp-verbose 6)
   #+END_SRC

   Will create a detailed log buffer.

* uniquify

  Name multiple identical buffer names in a sensible manner.

  #+BEGIN_SRC emacs-lisp
    (require 'uniquify)

    (setq uniquify-buffer-name-style 'forward)
  #+END_SRC

* windmove

  #+BEGIN_SRC emacs-lisp
    (require 'windmove)

    (windmove-default-keybindings 'hyper)
    (setq windmove-wrap-around t)

    (global-set-key (kbd "H-SPC") 'windmove-right)
    (global-set-key (kbd "H-S-SPC") 'windmove-left)
  #+END_SRC

* web-mode

  #+BEGIN_SRC emacs-lisp
    (require 'web-mode)

    (add-to-list 'auto-mode-alist '("\\.html?\\'" . web-mode))

    (defun k20e/web-mode-hook ()
      (set-default 'web-mode-engines-alist '(("django" . "\\.html?\\'")))
      (setq web-mode-markup-indent-offset 2
            web-mode-enable-auto-quoting nil))

    (add-hook 'web-mode-hook 'k20e/web-mode-hook)
  #+END_SRC

* winner-mode

  Remember window configurations.

  #+BEGIN_SRC emacs-lisp
    (require 'winner)

    (winner-mode)
  #+END_SRC

* whitespace

  Take care of some whitespace issues.

  - Kill trailing whitespace on save
  - Insert a new line at the end of file on save
  - Prefer =space= over =tab=

  #+BEGIN_SRC emacs-lisp
    (add-hook 'before-save-hook 'delete-trailing-whitespace)

    (set-default 'indent-tabs-mode nil)

    (setq require-final-newline t
          mode-require-final-newline t)
  #+END_SRC

* writeroom-mode

  Hack in a scale adjustment and alter width to fit.  There's likely a
  much better way to accomplish this.

  #+BEGIN_SRC emacs-lisp
    (require 'face-remap)
    (require 'writeroom-mode)

    (setq writeroom-width 126)

    (defvar k20e/writeroom-text-scale-amount 0
      "Text scale amount for `writeroom-mode'.")

    (defun k20e/writeroom-text-scale (arg)
      (require 'face-remap)
      (if arg (progn
                (set (make-local-variable 'k20e/writeroom-text-scale-amount)
                     text-scale-mode-amount)
                (text-scale-set 3))
        (text-scale-set
         (if (boundp 'k20e/writeroom-text-scale-amount)
             k20e/writeroom-text-scale-amount 0))))

    (add-to-list 'writeroom-global-effects 'k20e/writeroom-text-scale)
  #+END_SRC

* yaml-mode

  #+BEGIN_SRC emacs-lisp
    (require 'ansible-doc)
    (require 'yaml-mode)
    (require 'flycheck)
    (require 'flycheck-yamllint)
    (require 'flyspell)

    (defun k20e/yaml-mode-hook ()
      (setq fill-column 118)
      (ansible-doc-mode)
      (define-key yaml-mode-map (kbd "C-m") 'newline-and-indent)
      (flycheck-mode 1)
      (flycheck-yamllint-setup)
      (flyspell-mode-off))

    (add-hook 'yaml-mode-hook 'k20e/yaml-mode-hook)

    (add-to-list 'auto-mode-alist '("\\.yml$" . yaml-mode))
  #+END_SRC
